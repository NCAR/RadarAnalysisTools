      SUBROUTINE SETDCD(ITT,IDSEND,N,NAMF,ISYM,NDSYM,MAXSYM,KRD,NST)
C
C        ESTABLISHES PARAMETERIZATION FOR CODED DISPLAYS AND HISTOGRAMS
C                    IF ITT = 0, THE DISTRIBUTION TABLE WILL BE DEFINED
C                                WITHIN THE CONTEXT OF A HISTOGRAM
C                       ITT < 0, FULL DEFINITION BUT NO PRESENTATION
C                                ALSO KRD POSITIONS ARE SHIFTED BY 1.
C                       ITT > 0, FULL DISTRIBUTION TABLE WILL BE PRESENTED
C

      INCLUDE 'CEDRIC.INC'      
      PARAMETER (MAXBIN=1003)
      CHARACTER*8 OOPT,OOPTA,IMED,ICOL
      COMMON /SETWND/ ISETW(2,3),PSETW(2,3),ISETFL(NFMAX)
      COMMON /VOLUME/ INPID(NID),ID(NID),NAMFF(4,NFMAX),SCLFLD(NFMAX),
     X     IRCP(NFMAX),MAPVID(NFMAX,2),CSP(3,3),NCX(3),
     X     NCXORD(3),NFL,NPLANE,BAD
      CHARACTER*2 NAMFF
      COMMON /HSTOPT/ IMED,ICOL,TPER,OOPT,IMEDA(NFMAX),ICOLA(NFMAX),
     X     TPERA(NFMAX),OOPTA(NFMAX),FDORD(NFMAX),ICUR,SYMCD(2,NFMAX)
      COMMON /DSPECS/ IWIND(2,3),PWIND(2,3),NCWORD(3),IFLDW(NFMAX),
     X                SCLFAC(NFMAX),NSYMCD(2,NFMAX),WIDCD(NFMAX),
     X                ZREFCD(NFMAX),THKLIN(NFMAX)
      DIMENSION IDSEND(1),MAXSYM(2),TRNG(10)
      CHARACTER*(*) KRD(10)
      DIMENSION ICOLMAP(9)
      CHARACTER*8 CTEMP,NCOLORS(9),NAMIN
      CHARACTER*2 NAME(4),NAMF(4,NFMAX)
      CHARACTER*3 IAPS(3),ITEST
      CHARACTER*1 CIDF
      CHARACTER IPORN(2)*10,ISYM(NDSYM,2)*1,IST(10)*1,IBL*1
      DATA IPORN/' LEFT SIDE','RIGHT SIDE' /
      DATA IAPS/'PRI','SEC','ALL'/
      DATA IBL/' '/
      DATA CIDF/' '/
      DATA EPS/1.E-6/
      DATA NCOLORS / 'WHITE', 'GREY', 'PURPLE', 'BLUE', 'GREEN', 
     X               'YELLOW','BROWN','MAGENTA','RED' /
      DATA ICOLMAP /1,61,5,8,25,38,32,2,60/
 
C
C        DETERMINE PARAMETERIZATION FOR THIS FIELD SPECIFICATION
C
      MAXHST=MAXBIN
      NST=0
      K=1
      IF(ITT.LT.0) K=2
      IF(ITT.EQ.0) THEN
C
C        HISTOGRAM COMMAND
C
 50   FORMAT(A8)
      NAMIN=KRD(2)
      HMIN=0.0
      HMAX=100.0
      HINC=5.0
      IF(KRD(4).NE.CIDF) READ (KRD(4),101)HMIN
      IF(KRD(5).NE.CIDF) READ (KRD(5),101)HMAX
      IF(KRD(6).NE.CIDF) READ (KRD(6),101)HINC
  101 FORMAT(F8.0)
      IF(HINC.LE.0.0) HINC=5.0
      IF(HMAX.LT.HMIN)HMAX=HMIN+100.0
      NH=(HMAX-HMIN)/HINC+EPS
      IF(NH.GT.MAXHST) THEN
         NH=MAXHST
         HMAX=HMIN+(HINC*NH)
      END IF
      ELSE
C
C        CODED DISPLAY TABLE
C
         IF (K.EQ.2) THEN
            READ (KRD,102)NAMIN,REF,WID,RNLFT,RNRGT
 102        FORMAT(//A8//F8.0/F8.0/F8.0/F8.0)
         ELSE IF (K.EQ.1) THEN
            READ(KRD,103)NAMIN,REF,WID,RNLFT,RNRGT
 103        FORMAT(/A8//F8.0/F8.0/F8.0/F8.0)
         ELSE
            WRITE(*,114)K
 114        FORMAT(/,5X,'+++INVALID K IN SETDCD+++'/)
            CALL FLUSH_STDOUT
         END IF
         IF(WID.LE.0.0) WID=5.0
         NLFT=RNLFT
         NRGT=RNRGT
      END IF
C
C        DETERMINE FIELD(S) TO BE PARAMETERIZED
C
      WRITE (CTEMP,20)NAMIN
      READ (CTEMP,100)ITEST
  100 FORMAT(A3)
      LF=IFINDC(ITEST,IAPS,3,0)
      IF(NAMIN.EQ.' ') LF=1
      IF(LF.EQ.0) GO TO 10
C
C        GROUP OF FIELDS
C
      IF(LF.EQ.3) THEN
         J=IFINOT(0,ISETFL,NFMAX,0)
      ELSE
         J=IFIND(LF,ISETFL,NFMAX,0)
      END IF
      IF(J.EQ.0) GO TO 90
      IF (OOPT.EQ.'O') THEN
         WRITE(*,117)
 117     FORMAT(5X,'+++CANNOT HAVE OVERLAYS WITH MULT. FIELD ',
     X          'SPECIFICATIONS--NORMAL DISPLAY CREATED+++')
         OOPT='D'
      END IF
      GO TO 15
   10 CONTINUE
C
C        INDIVIDUAL FIELD
C
      WRITE (CTEMP,20)NAMIN
 20   FORMAT(A8)
      READ (CTEMP,104)NAME
  104 FORMAT(4A2)
      J=LOCFLD(NAME,NAMF,4,NFMAX,4)
      IF(J.EQ.0) THEN
         CALL CEDERX(507,0)
         NST=1
         GO TO 90
      END IF
C
C     USE MAPVID TO FIND A FIELD'S LOCATION IN MEMORY GIVEN ITS NAME
C
      FDORD(ICUR)=MAPVID(J,1)
      IF (FDORD(ICUR).EQ.0) THEN
         CALL CEDERX(507,1)
         NST=1
         RETURN
      END IF
      ICUR=ICUR+1
   15 CONTINUE
      IF(ITT.NE.0) GO TO 25
C
C        HISTOGRAM
C
         NEG=NH/2
         SYMCD(1,J)=HMIN
         SYMCD(2,J)=HMAX
         WIDCD(J)=HINC
         ZREFCD(J)=HMIN+(NEG*HINC)
         IF (IMED.EQ.'ON') THEN
            IMEDA(J)=1
         ELSE
            IMEDA(J)=0
         END IF
         IF (ICOL.NE.' ' .AND.ICOL.NE.'NONE') THEN
            NCOL=LOCFLD(ICOL,NCOLORS,1,9,1)
            IF (NCOL.GT.0) THEN
               ICOLA(J)=ICOLMAP(NCOL)
            ELSE
               ICOLA(J)=-1
            END IF
         ELSE
            ICOLA(J)=-1
         END IF
         TPERA(J)=TPER
         IF (OOPT.NE. ' ') THEN
            OOPTA(J)=OOPT
         ELSE
            OOPTA(J)='LINEAR'
         END IF
         GO TO 35
   25 CONTINUE
C
C        CODED DISPLAY
C
      WIDCD(J)=WID
      ZREFCD(J)=REF
      NSYMCD(1,J)=MIN0(NLFT,MAXSYM(1))
      NSYMCD(2,J)=MIN0(NRGT,MAXSYM(2))
      IF(ITT.LT.0) GO TO 35
      DO 30 LOUT=1,N
      IDEV=IDSEND(LOUT)
C
C        PRESENT THE FULL DISTRIBUTION TABLE
C
      WRITE(IDEV,105) NAMIN
  105 FORMAT(/5X,'FIELD NAME: ',A8,8X,'DISTRIBUTION TABLE:'/)
      WRITE(IDEV,106) ZREFCD(J),WIDCD(J)
  106 FORMAT(' REFERENCE VALUE=',F8.2/'  INTERVAL WIDTH=',F8.2)
      WAD=WIDCD(J)
      DO 29 K=1,2
      WAD= -WAD
      WRITE(IDEV,110) NSYMCD(K,J), IPORN(K)
  110 FORMAT(/1X,I2,' ',A10,' INTERVALS:')
      IF(NSYMCD(K,J).EQ.0) THEN
        WRITE(IDEV,115) IPORN(K), ISYM(NDSYM,K)
  115   FORMAT(/5X,A10,' VALUES WILL BE REPRESENTED WITH A ',A1)
      ELSE
        LOOP=(NSYMCD(K,J)-1)/9 + 1
        RINT=ZREFCD(J)
        ISI=0
        LEFT=10
        LEFTM1=9
        IST(10)=IBL
        DO 28 L=1,LOOP
        IF(L.LT.LOOP) GO TO 26
        LEFTM1=NSYMCD(K,J) - (L-1)*9
        LEFT=LEFTM1+1
        IST(LEFT)=ISYM(NDSYM,K)
   26   CONTINUE
        TRNG(LEFT)=RINT+(LEFTM1)*WAD
        DO 27 I=1,LEFTM1
        ISI=ISI+1
        TRNG(I)=RINT
        IST(I)=ISYM(ISI,K)
        RINT=RINT+WAD
   27   CONTINUE
        WRITE(IDEV,111) (TRNG(I), I=1,LEFT)
  111   FORMAT(/5X,'RANGE:',4X,10F6.1)
        WRITE(IDEV,112) (IST(I), I=1,LEFT)
  112   FORMAT(5X,'SYMBOL:',4X,10(5X,A1))
   28   CONTINUE
      END IF
   29 CONTINUE
   30 CONTINUE
   35 CONTINUE
C
C        WRAP UP HERE- COPY INFO TO ALL AFFECTED FIELDS IF LF NON-ZERO
C
      IF(LF.EQ.0) GO TO 90
      DO 40 L=1,NFMAX
         IF(ISETFL(L).EQ.0) GO TO 40
         IF(LF.NE.3.AND.LF.NE.ISETFL(L)) GO TO 40
         NSYMCD(1,L)=NSYMCD(1,J)
         NSYMCD(2,L)=NSYMCD(2,J)
         SYMCD(1,L)=SYMCD(1,J)
         SYMCD(2,L)=SYMCD(2,J)
         WIDCD(L)=WIDCD(J)
         ZREFCD(L)=ZREFCD(J)
         TPERA(L)=TPERA(J)
         OOPTA(L)=OOPTA(J)
         IMEDA(L)=IMEDA(J)
         ICOLA(L)=ICOLA(J)
   40 CONTINUE
   90 CONTINUE
      RETURN
      END
