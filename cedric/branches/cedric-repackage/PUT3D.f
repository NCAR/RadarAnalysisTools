      SUBROUTINE PUT3D(IEDFL,IBUF,OBUF,NX,NY,NZ,IFLD,JFLD,
     X                 NEWJ,MISV,A,IWIND,NST)
C
C        PLACES THE DATA FROM A 3-D WINDOWED REGION BACK ONTO THE
C               EXTERNAL STORAGE MEDIUM.
C               INVERSE OPERATION OF GET3D.
C
C       IEDFL- LOGICAL UNIT NUMBER OF THE EDIT FILE
C        IBUF- SCRATCH BUFFER FOR I/O TRANSFER
C        OBUF- 2-D ARRAY USED TO TRANSFER THE DATA FROM INDIVIDUAL PLANES
C          NX- NUMBER OF POINTS ALONG THE X-AXIS (FULL VOLUME)
C          NY- NUMBER OF POINTS ALONG THE Y-AXIS (FULL VOLUME)
C          NZ- NUMBER OF POINTS ALONG THE Z-AXIS (FULL VOLUME)
C        IFLD- INDEX OF THE  INPUT FIELD IN THE ID HEADER
C        JFLD- INDEX OF THE OUTPUT FIELD IN THE ID HEADER
C        NEWJ- LOGICAL FLAG THAT IS SET TRUE WHENEVER JFLD IS NEWLY CREATED
C        MISV- LOGICAL FLAG SET TO TRUE IF OUTPUT FIELD IS TO HAVE
C              MISSING VALUES AT SAME LOCATION OF THE INPUT FIELD.
C           A- BUFFER CONTAINING DATA FROM THE 3-D WINDOWED REGION
C       IWIND- INDICES OF THE WINDOWED REGION IN CONTEXT OF THE FULL VOLUME
C         NST- STATUS FLAG; =0 OK, =1 I/O TRANSFER PROBLEMS.
C
C
      INCLUDE 'CEDRIC.INC'
      DIMENSION A(1),IBUF(1),OBUF(NX,NY),IWIND(2,3)
      COMMON /VOLUME/ INPID(NID),ID(NID),NAMF(4,NFMAX),SCLFLD(NFMAX),
     X                IRCP(NFMAX),MAPVID(NFMAX,2),CSP(3,3),NCX(3),
     X                NCXORD(3),NFL,NPLANE,BAD
      CHARACTER*2 NAMF
      LOGICAL NEWJ,MISV,IDIFXY
C
C        GET INDICES OF THE WINDOWED REGION
C
      I1=IWIND(1,1)
      I2=IWIND(2,1)
      J1=IWIND(1,2)
      J2=IWIND(2,2)
      K1=IWIND(1,3)
      K2=IWIND(2,3)
C
C        SET IDIFXY TO FALSE IF (X,Y) WINDOW MATCHES THE FULL GRID
C
      IDIFXY=.TRUE.
      IF(I1.EQ.1.AND.I2.EQ.NCX(1).AND.
     X   J1.EQ.1.AND.J2.EQ.NCX(2)) IDIFXY=.FALSE.
C
C        RESTORE INFO IN THE (X,Y,Z) ARRAY TO STORAGE
C
      L=0
      DO 30 K=1,NZ
         IF(K.LT.K1.OR.K.GT.K2) THEN
C
C           OUTSIDE OF Z-WINDOW
C
            IF(.NOT.NEWJ) GO TO 30
            CALL FETCHD(IEDFL,ID,K,IFLD,IBUF,OBUF,NIX,NIY,
     X                  3,BAD,ZLEV,NST)
            IF(NST.NE.0) GO TO 90
         ELSE
C
C           INSIDE THE Z-WINDOW, FETCH THIS Z-PLANE IF (X,Y) GRIDS DIFFER
C
            IF(IDIFXY.OR.MISV) THEN
               CALL FETCHD(IEDFL,ID,K,IFLD,IBUF,OBUF,NIX,NIY,
     X                     3,BAD,ZLEV,NST)
               IF(NST.NE.0) GO TO 90
            END IF
C
C           MOVE DATA FROM 3-D GRID TO CURRENT PLANE
C
            DO 20 J=J1,J2
               DO 15 I=I1,I2
                  L=L+1
                  IF(MISV.AND.(OBUF(I,J).EQ.BAD)) GO TO 15
                  OBUF(I,J)=A(L)
   15          CONTINUE
   20       CONTINUE
         END IF
C
C        STORE THE DATA FOR THIS PLANE
C
         CALL PLACED(IEDFL,ID,K,JFLD,IBUF,OBUF,NX,NY,
     X               3,BAD,NST)
         IF(NST.NE.0) GO TO 90
C
   30 CONTINUE
C
C        RETURN BRANCH
C
   90 CONTINUE
      RETURN
      END
