      SUBROUTINE UNFSLP(RBUF,SBUF,OBUF,NX,NY,IBEG,IEND,JBEG,JEND,
     X                  VNYQ,BAD,MODE)
C
C        AUTOMATIC UNFOLDING WITH SLOPE PRESERVATION
C           MODE=0, UNFOLD AND UPDATE TEMPLATE
C               =1, UPDATE TEMPLATE ONLY
C
      DIMENSION RBUF(NX,NY),SBUF(NX,NY),OBUF(NX,NY)
      DATA EPS/0.01/
C
C        DATA-FILL VELOCITY FIELD FROM LAST PLANE
C
      CALL EXTEND(SBUF,NX,NY,1,BAD)
      IF(MODE.EQ.1) GO TO 96
C
C        DATA FILL TEMPLATE AND PROCEED WITH UNFOLDING
C
      CALL EXTEND(OBUF,NX,NY,1,BAD)
      VN2=VNYQ*2.0
C
C           COMPARE ESTIMATES AND UNFOLD IF WARRANTED
C
            DO 95 J=JBEG,JEND
            DO 95 I=IBEG,IEND
               VREF=OBUF(I,J)
               IF(VREF.EQ.BAD) GO TO 95
               VEL=RBUF(I,J)
               IF(VEL.EQ.BAD) GO TO 95
               DIF=VREF-VEL
               IF(ABS(DIF).LE.VNYQ) GO TO 95
               K=(DIF+SIGN(VNYQ-EPS,DIF))/VN2
               RBUF(I,J)=VEL+K*VN2
   95       CONTINUE
   96    CONTINUE
C
C        UPDATE THE TEMPLATE AND LAST PLANE VELOCITY FIELD
C
         DO 98 J=1,NY
         DO 98 I=1,NX
            OBUF(I,J)=BAD
            IF(RBUF(I,J).EQ.BAD.OR.SBUF(I,J).EQ.BAD) GO TO 97
            OBUF(I,J)=2.0*RBUF(I,J)-SBUF(I,J)
   97       CONTINUE
            SBUF(I,J)=RBUF(I,J)
   98    CONTINUE
      RETURN
      END
