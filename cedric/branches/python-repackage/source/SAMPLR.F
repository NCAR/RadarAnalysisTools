      SUBROUTINE SAMPLR(KRD,IBUF,OBUF,RBUF,IPR,NST)
C
C        DRIVER FOR THE GENERATION OF A RADIAL VELOCITY FIELD FROM
C               U, V AND W COMPONENT INPUT FIELDS.
C
      INCLUDE 'CEDRIC.INC'
      DIMENSION IBUF(MAXPLN),OBUF(MAXPLN),RBUF(MAXPLN,3),IFLD(3),RLOC(3)
      DIMENSION CON(3)
      CHARACTER*(*) KRD(10)
      COMMON /AXUNTS/ IUNAXS,LABAXS(3,3),SCLAXS(3,3),AXNAM(3)
      CHARACTER*4 AXNAM,CTEMP
      CHARACTER*5 CORD
      COMMON /VOLUME/ INPID(NID),ID(NID),NAMF(4,NFMAX),SCLFLD(NFMAX),
     X                IRCP(NFMAX),MAPVID(NFMAX,2),CSP(3,3),NCX(3),
     X                NCXORD(3),NFL,NPLANE,BAD
      CHARACTER*2 NAMF,NAMOUF(4),NAMINF(4,3),IBL
      CHARACTER*1 IWOP,NUVW(3)
      COMMON /EDINFO/ IEDW(2,3),PEDW(2,3)
      LOGICAL NEWJ
      EQUIVALENCE (NCX(1),NX),(NCX(2),NY),(NCX(3),NZ)
      DATA NUVW/'U','V','W'/
      DATA IBL/'  '/
      DATA ISCL/100/
C
C        INITIALIZATION
C
      WRITE(CTEMP,13)ID(16),ID(17)
 13   FORMAT(2A2)
      READ (KRD,100)NAMOUF,NAMINF,RLOC,CORD,IWOP
C  100 FORMAT(8X,16A2,3F8.0,8X,A1)
 100  FORMAT(/4A2/4A2/4A2/4A2/F8.0/F8.0/F8.0/A5/A1)
      IF (CORD.EQ.'NORTH') THEN
         ICOR=1
      ELSE
         ICOR=0
      END IF
      I=IADFLD(NAMOUF,ISCL,IPR)
      IF (I.LT.0) RETURN
      NEWJ=.FALSE.
      IF(I.NE.0) NEWJ=.TRUE.
      JFLD=LOCFLDID(NAMOUF,ID(176),5,NFL,4)
      CON(1)=FLOAT(ID(40))/FLOAT(ID(69))
      SF=1./FLOAT(ID(68))
      CON(2)=ID(41)*SF
      CON(3)=ID(42)*SF
      CALL WINSET(IEDW,PEDW,IWOP)
      CALL SHOEDW(IPR)
      CALL SHOEDF(IPR)
      WRITE(IPR,101)(RLOC(I),I=1,3),LABAXS(3,1),
     X     (NUVW(J),(NAMINF(I,J),I=1,4),J=1,3),(NAMOUF(I),I=1,4)
  101 FORMAT(' (X,Y,Z) RADAR SAMPLING LOCATION:  ',
     X       '(',F8.3,',',F8.3,',',F8.3,')   ',A4,'.'/
     X 9X,A2,'COMPONENT INPUT FIELD: ',4A2/
     X 9X,A2,'COMPONENT INPUT FIELD: ',4A2/
     X 9X,A2,'COMPONENT INPUT FIELD: ',4A2/
     X 4X,'RADIAL VELOCITY OUTPUT FIELD: ',4A2)
      DO 3 I=1,3
         IF(NAMINF(1,I).EQ.IBL) THEN
            IFLD(I)=0
            WRITE(IPR,111) NUVW(I)
  111       FORMAT(5X,A1,' COMPONENT FIELD WILL BE ASSUMED=0.')
            CALL CONFLD(RBUF(1,I),NPLANE,0.0)
         ELSE
            IFLD(I)=LOCFLDID(NAMINF(1,I),ID(176),5,NFL,4)
            IF(IFLD(I).EQ.0) THEN
               CALL CEDERX(501,1)
               RETURN
            END IF
         END IF
    3 CONTINUE
      JBEG=IEDW(1,2)
      JEND=IEDW(2,2)
      IBEG=IEDW(1,1)
      IEND=IEDW(2,1)
C
C        PARAMETERIZATION IS O.K... EXECUTE SAMPLING PROCEDURE.
C
C
C        LOOP FOR EACH LEVEL
C
      DO 59 LEV=1,NZ
         IF(LEV.GE.IEDW(1,3).AND.LEV.LE.IEDW(2,3)) GO TO 55
C
C        PLANE IS OUTSIDE EDITING WINDOW
C
         IF(.NOT.NEWJ) GO TO 59
            CALL CONFLD(OBUF,NPLANE,BAD)
            GO TO 58
   55    CONTINUE
C
C        PLANE IS INSIDE VERTICAL WINDOW
C              INITIALIZE OUTPUT BUFFER.
C
         IF(NEWJ) THEN
            CALL CONFLD(OBUF,NPLANE,BAD)
         ELSE
            CALL FETCHD(IEDFL,ID,LEV,JFLD,IBUF,OBUF,
     X                  NIX,NIY,3,BAD,RLEV,NST)
            IF(NST.NE.0) GO TO 90
         END IF
         DO 57 I=1,3
            IF(IFLD(I).EQ.0) GO TO 57
C
C           FETCH THE NEXT INPUT FIELD
C
            CALL FETCHD(IEDFL,ID,LEV,IFLD(I),IBUF,RBUF(1,I),
     X                  NIX,NIY,3,BAD,RLEV,NST)
            IF(NST.NE.0) GO TO 90
            IF(I.NE.3) GO TO 57
C
C              SET BAD W-VALUES TO 0.0
C
               DO 56 L=1,NPLANE
                  IF(RBUF(L,3).EQ.BAD) RBUF(L,3)=0.0
   56          CONTINUE
   57    CONTINUE
         ZLEV=CSP(1,3)+(LEV-1)*CSP(3,3)
         CALL DESYNR(OBUF,RBUF(1,1),RBUF(1,2),RBUF(1,3),NX,NY,
     X               IBEG,IEND,JBEG,JEND,ZLEV,CSP,RLOC,CON,BAD,ICOR,
     X               CTEMP,AXNAM(3))
   58    CONTINUE
         CALL PLACED(IEDFL,ID,LEV,JFLD,IBUF,OBUF,NX,NY,3,BAD,NST)
         IF(NST.NE.0) GO TO 90
   59 CONTINUE
   90 CONTINUE
C
C        NORMAL TERMINATION
C
      RETURN
      END
