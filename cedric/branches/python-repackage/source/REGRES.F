      SUBROUTINE REGRES(KRD,IBUF,RBUF,IPR)
C
C        DRIVER FOR REGRESSION OF ONE FIELD VERSUS ANOTHER
C        LISTING IS GENERATED.
C

      INCLUDE 'CEDRIC.INC'
      COMMON /VOLUME/ INPID(NID),ID(NID),NAMF(4,NFMAX),SCLFLD(NFMAX),
     X                IRCP(NFMAX),MAPVID(NFMAX,2),CSP(3,3),NCX(3),
     X                NCXORD(3),NFL,NPLANE,BAD
      CHARACTER*2 NAMF,NAMX(4),NAMY(4)
C
      COMMON /DSPECS/ IWIND(2,3),PWIND(2,3),NCWORD(3),IFLDW(NFMAX),
     X                SCLFAC(NFMAX),NSYMCD(2,NFMAX),WIDCD(NFMAX),
     X                ZREFCD(NFMAX),THKLIN(NFMAX)
C
      COMMON /AXUNTS/ IUNAXS,LABAXS(3,3),SCLAXS(3,3),AXNAM(3)
      CHARACTER*4 AXNAM
C
      DIMENSION IBUF(MAXPLN),RBUF(MAXPLN,2),IDSEND(2)
      CHARACTER*(*) KRD(10)
      CHARACTER*1 IPVB(3),IC,LF,IDEST,IWOP
C
      DATA IPVB/'P','V','B'/
C
C     DETERMINE THE PARAMETERIZATION
C
      READ (KRD,120)IDEST,LF,RSKP,IC,NAMX,NAMY,IWOP
C  120 FORMAT(8X,A1,7X,A1,7X,F8.0,A1,7X,8A2,16X,A1)
 120  FORMAT(/A1/A1/F8.0/A1/4A2/4A2///A1)
C
      LSKP=MAX1(RSKP,1.0)
      ITPD=LOCATE(IC,IPVB,3)
      IF(ITPD.LE.0) ITPD=3
      CALL WINSET(IWIND,PWIND,IWOP)
      CALL SETAXS(NCWORD,LF,IPR)
      CALL SELDEV(IDSEND,N,IDEST)
      IXFL=LOCFLDID(NAMX,ID(176),5,NFL,4)
      IYFL=LOCFLDID(NAMY,ID(176),5,NFL,4)
      IF(IXFL.EQ.0.OR.IYFL.EQ.0) THEN
         CALL CEDERX(501,1)
         RETURN
      END IF
C
      I1=NCWORD(1)
      I2=NCWORD(2)
      I3=NCWORD(3)
      L1=IWIND(1,I3)
      L2=IWIND(2,I3)
      IFIXAX=NCWORD(3)
C
      DO 50 M=1,N
         IOUT=IDSEND(M)
         WRITE(IOUT,121)IWIND(1,I1),IWIND(2,I1),PWIND(1,I1),PWIND(2,I1),
     XLABAXS(I1,IUNAXS),IWIND(1,I2),IWIND(2,I2),PWIND(1,I2),PWIND(2,I2),
     XLABAXS(I2,IUNAXS)
         WRITE(99,121)IWIND(1,I1),IWIND(2,I1),PWIND(1,I1),PWIND(2,I1),
     XLABAXS(I1,IUNAXS),IWIND(1,I2),IWIND(2,I2),PWIND(1,I2),PWIND(2,I2),
     XLABAXS(I2,IUNAXS)
  121 FORMAT(/9X,'REGRESSION IS FROM (',I3,' TO ',I3,')   (',F7.2,
     X       ' TO ',F7.2,1X,A4,') ALONG I'/28X,'(',I3,' TO ',I3,')   (',
     X       F7.2,' TO ',F7.2,1X,A4,') ALONG J')
         IF(ITPD.GT.1) THEN
           WRITE(IOUT,106) IWIND(1,I3),IWIND(2,I3),
     X                     PWIND(1,I3),PWIND(2,I3),LABAXS(I3,IUNAXS)
           WRITE(99,106) IWIND(1,I3),IWIND(2,I3),
     X                     PWIND(1,I3),PWIND(2,I3),LABAXS(I3,IUNAXS)
  106      FORMAT(19X,'         (',I3,' TO ',I3,')   (',F7.2,' TO ',
     X                F7.2,1X,A4,') ALONG K')
         END IF
         WRITE(IOUT,107)
         WRITE(99,107)
  107    FORMAT(/)
   50 CONTINUE
C
C        INITIALIZE THE VOLUME ACCUMULATORS AND LIST THE HEADER
C
      CALL REGRZI
      DO 51 M=1,N
         CALL REGINT(NAMX,NAMY,IFIXAX,AXNAM(IFIXAX),IDSEND(M))
   51 CONTINUE
C
C        LOOP THROUGH THE REQUESTED LEVELS
C
      DO 55 L=L1,L2
         IGO=MOD(L-L1,LSKP)
         IF(IGO.NE.0.AND.ITPD.EQ.1) GO TO 55
         CALL FETCHD(IN,ID,L,IXFL,IBUF,RBUF(1,1),N1,N2,
     X               IFIXAX,BAD,RLEV,NST)
         IF(NST.NE.0) GO TO 90
         CALL FETCHD(IN,ID,L,IYFL,IBUF,RBUF(1,2),N1,N2,
     X               IFIXAX,BAD,RLEV,NST)
         IF(NST.NE.0) GO TO 90
c         RLEV=CSP(1,IFIXAX)+(L-1)*CSP(3,IFIXAX)
         RLEV=RLEV*SCLAXS(IFIXAX,IUNAXS)
         CALL REGCAL(RBUF(1,1),RBUF(1,2),N1,N2,IWIND,NCWORD,BAD,
     X               NP,FMNX,STDX,FMNY,STDY,CCF,STDERR,C0,C1,1)
         IF(IGO.EQ.0.AND.ITPD.NE.2) THEN
C
C           LIST OUT THE INFO FOR THIS LEVEL
C
            DO 53 M=1,N
               IOUT=IDSEND(M)
               WRITE(IOUT,122) RLEV,NP,FMNX,STDX,FMNY,STDY,CCF,
     X                         STDERR,C0,C1
               WRITE(99,122) RLEV,NP,FMNX,STDX,FMNY,STDY,CCF,
     X                         STDERR,C0,C1
  122          FORMAT(1X,F7.2,I7,F9.2,F8.2,F9.2,F8.2,F6.3,F8.2,2F8.3)
   53       CONTINUE
         END IF
C
   55 CONTINUE
C
      IF(ITPD.NE.1) THEN
C
C        LIST OUT THE VOLUME STATS
C
         CALL REGCAL(RBUF(1,1),RBUF(1,2),N1,N2,IWIND,NCWORD,BAD,
     X               NP,FMNX,STDX,FMNY,STDY,CCF,STDERR,C0,C1,2)
         DO 60 M=1,N
            IOUT=IDSEND(M)
            WRITE(IOUT,123) NP,FMNX,STDX,FMNY,STDY,CCF,STDERR,C0,C1
            WRITE(99,123) NP,FMNX,STDX,FMNY,STDY,CCF,STDERR,C0,C1
  123       FORMAT(2X,'VOLUME',I7,F9.2,F8.2,F9.2,F8.2,F6.3,F8.2,2F8.3)
   60    CONTINUE
C
      END IF
C
   90 CONTINUE
      RETURN
      END
