      SUBROUTINE CREATE(IPR,KRD)
C
C        DEFINES A NEW CARTESIAN (X,Y,Z) I/O MAPPING
C
      INCLUDE 'CEDRIC.INC'
      COMMON /CREAXS/ CSPN(3,3),NCXN(3),NPLNEW
      COMMON /AXUNTS/ IUNAXS,LABAXS(3,3),SCLAXS(3,3),AXNAM(3)
      CHARACTER*4 AXNAM
      COMMON /LEVELS/ VALLEV(MAXZLEV),VALNYQ(MAXZLEV),VNYQ_VOL
      DIMENSION MAXAX(3),LAX(3),R(3)
      CHARACTER*(*) KRD(10)
      CHARACTER*3 ITEST,IOFF
      DATA CSPN/9*0.0/
      DATA NCXN/3*0/
      DATA NPLNEW/0/
      DATA MAXAX/MAXX,MAXY,MAXZLEV/
      DATA LAX/'X','Y','Z'/
      DATA IOFF/'OFF'/
      READ (KRD,101)ITEST
C  101 FORMAT(8X,A3)
 101  FORMAT(/A3)
      IF(ITEST.NE.IOFF) GO TO 20
C
C        TURN OFF THE CURRENT MAPPING
C
      DO 10 J=1,3
         NCXN(J)=0
         DO 5 I=1,3
            CSPN(I,J)=0.0
    5    CONTINUE
   10 CONTINUE
      NPLNEW=0
      GO TO 50
   20 CONTINUE
C
C        ESTABLISH A NEW MAPPING
C
      READ (KRD,102)((CSPN(I,J),I=1,3),J=1,3)
C  102 FORMAT(8X,9F8.0)
 102  FORMAT(/F8.0/F8.0/F8.0/F8.0/F8.0/F8.0/F8.0,
     X     /F8.0/F8.0)
      DO 30 J=1,3
C
C         SURFACE AND SINGLE PLANE DATA
C
          TEST=CSPN(3,J)
          IF(TEST.LT.0.0) THEN
             DIV=1.0
             CSPN(3,J) = -0.001
          ELSE IF(TEST.EQ.0.0) THEN
             DIV=1.0
          ELSE
             DIV=1./CSPN(3,J)
          END IF
C
         RNUM=(CSPN(2,J)-CSPN(1,J))*DIV+1.001
         NCXN(J)=RNUM
         IF(ABS(FLOAT(NCXN(J))-RNUM).GT.0.01) THEN
            WRITE(IPR,200) AXNAM(J)
  200       FORMAT(5X,'+++  ',A2,'AXIS MUST CONTAIN AN INTEGRAL',
     X                ' NUMBER OF POINTS  +++')
            CALL CEDERX(503,1)
            NPLNEW=0
            RETURN
         END IF
         IF(NCXN(J).EQ.1.AND.CSPN(3,J).GT.0.0) CSPN(3,J)=0.0
         IF(NCXN(J).NE.1.AND.CSPN(3,J).LE.0.0) THEN
            WRITE(IPR,201) AXNAM(J)
  201       FORMAT(5X,'+++  ',A2,'AXIS SPACING MUST BE POSITIVE  +++')
            CALL CEDERX(503,1)
            NPLNEW=0
            RETURN
         END IF
      IF(J.EQ.3.AND.(NCXN(J).LE.0.OR.NCXN(J).GT.MAXAX(J))) THEN
         WRITE(IPR,202) AXNAM(J),MAXAX(J)
  202    FORMAT(5X,'+++  NUMBER OF POINTS ALONG THE  ',A2,
     X             'AXIS MUST BE IN THE RANGE OF  1 TO ',I3,'  +++')
         CALL CEDERX(503,1)
         NPLNEW=0
         RETURN
      END IF
   30 CONTINUE
      NPLNEW=NCXN(1)*NCXN(2)
      IF(NPLNEW.LE.0.OR.NPLNEW.GT.MAXPLN) THEN
         WRITE(IPR,203) MAXPLN
  203    FORMAT(5X,'+++  NUMBER OF POINTS/PLANE MUST BE IN THE ',
     X             'RANGE OF  1 TO ',I5,'  +++')
         CALL CEDERX(503,1)
         NPLNEW=0
         RETURN
      END IF
   50 CONTINUE
C
C        DISPLAY THE CARTESIAN COORDINATE SYSTEM STRUCTURE
C                DEFINED BY CSPN,NCXN AND NPLNEW.
C
      WRITE(IPR,103)
  103 FORMAT(' USER SUPPLIED CARTESIAN COORDINATE SPECIFICATIONS ...')
      IF(NPLNEW.LE.0) THEN
         WRITE(IPR,104)
  104    FORMAT(' USER SUPPLIED COORDINATE SYSTEM IS PRESENTLY',
     X          ' UNDEFINED.')
         RETURN
      END IF
      WRITE(IPR,105)
  105 FORMAT(/3X,'AXIS',4X,'MINIMUM',4X,'MAXIMUM',4X,'SPACING',
     X        4X,'UNITS',5X,'NO. OF PTS')
      DO 60 I=1,3
         R(1)=CSPN(1,I)*SCLAXS(I,IUNAXS)
         R(2)=CSPN(2,I)*SCLAXS(I,IUNAXS)
         R(3)=CSPN(3,I)*SCLAXS(I,IUNAXS)
         WRITE(IPR,106) AXNAM(I),(R(L),L=1,3),LABAXS(I,IUNAXS),NCXN(I)
  106    FORMAT(5X,A2,3F11.2,5X,A4,5X,I7)
   60 CONTINUE
      DO I=1,NCXN(3)
         VALLEV(I)=CSPN(3,3)*(I-1) + CSPN(1,3)
      END DO
      WRITE(IPR,107) NPLNEW
  107 FORMAT(3X,'PTS/PLANE= ',I8)
      RETURN
      END
