      SUBROUTINE VECDSP(IBUF,RBUF,WBUF,LEV,RLEV,PLWIND,NCWORD,IFLVEC,
     X                  ITIT,VECTIN,IFLASH,MXFLSH,ITYP,NST,NVECCOL,
     X                  ISKPI,ISKPJ,LABFLG)
C
C        GENERATES THE VECTOR DISPLAY PLOT (OVERLAY AND WITH LABELING)
C                  EITHER A VECTOR OR A STREAMLINE PLOT CAN BE PRODUCED
C                  DEPENDING UPON THE VALUE OF VECTIN(2)- SPEED REFERENCE.
C                  ITYP=ANYTHING BUT ZERO, OVERLAY ONLY
C                      =1, OVERLAY + FULL PLOT WITH LABELING
C                   NST=0, EVERYTHING O.K.
C                      =1, INSUFFICIENT PARAMETERIZATION
C
      INCLUDE 'CEDRIC.INC'      
      COMMON /AXSTRB/ AXSFAC(3)
      COMMON /VOLUME/ INPID(NID),ID(NID),NAMF(4,NFMAX),SCLFLD(NFMAX),
     X                IRCP(NFMAX),MAPVID(NFMAX,2),CSP(3,3),NCX(3),
     X                NCXORD(3),NFL,NPLANE,BAD
      CHARACTER*2 NAMF
      COMMON /AXUNTS/ IUNAXS,LABAXS(3,3),SCLAXS(3,3),AXNAM(3)
      CHARACTER*4 AXNAM
      COMMON /RANGEC/ XBEG,XEND,YBEG,YEND,XRANGE(2),YRANGE(2),DELRG(2)
      CHARACTER*1 IAXIS(3)
      CHARACTER*2 NAME(4,2)
      DIMENSION IBUF(1),RBUF(1),IFLASH(MXFLSH),PLWIND(2,3),NCWORD(3),
     X          IFLVEC(2),VECTIN(5),WBUF(1),VLBUF(7)
      CHARACTER*16 CFMTX,CFMTY
      CHARACTER*8 NOW,IFMTX(2),IFMTY(2),ITIT(5)
      CHARACTER*80 JTIT
      CHARACTER*40 CITIT
      DATA IAXIS/'X','Y','Z'/
      DATA VLBUF/20.,10.,5.,2.,1.,.5,.1/
      DATA IN/0/
      DIMENSION ICOLMAP(10)
      DATA ICOLMAP/1,61,5,8,25,38,32,2,60,0/

C     Patch to allow for turning off framing (LJM 7/30/2011)
C     Do this to get several radial velocities as vectors
C     onto a single plot.  To do this reset FRAMIT to .FALSE.
C
      LOGICAL FRAMIT
      DATA FRAMIT /.TRUE./

      LOCPLT(R)=1023.*R
      
      WRITE (CITIT,500)ITIT
 500  FORMAT(5A8)
C
C        INITIALIZE PARAMETERIZATION
C
      IVDENS=VECTIN(1)
      VMS=VECTIN(2)
C
C        PARAMETERIZATION IS SUFFICIENT- PROCEED WITH DISPLAY
C
      L1=NCWORD(1)
      L2=NCWORD(2)
      L3=NCWORD(3)
      LOC=1
C
C        FETCH IN THE TWO VECTOR FIELDS
C
      DO 3 I=1,2
         M=IFLVEC(I)
         CALL COPCX(NAME(1,I),NAMF(1,M),4)
         IFLD=MAPVID(M,1)
         CALL FETCHD(IN,ID,LEV,IFLD,IBUF,RBUF(LOC),N1,N2,
     X               L3,BAD,RLEV,NST)
         IF(NST.NE.0) RETURN
         LOC=LOC+N1*N2
    3 CONTINUE
      CALL RGINI(XL,XR,YB,YT,PLWIND,CSP,NCX,NCWORD)
C
C        SAVE THE VECTOR BACKGROUND IN IFLASH BUFFER
C
      CALL GFLAS1(1)
      CALL GSCLIP(0)
      WRITE (JTIT,102)((NAME(I,J),I=1,4),J=1,2),IVDENS
  102 FORMAT(4A2,',',4A2,' EVERY ',I2,' PT.')
      LOCY=LOCPLT(YB)-50
      IF (LABFLG.GT.5) THEN
         CALL PLCHMQ(CPUX(275),CPUY(LOCY),JTIT(1:30),12.,0.,-1.)
      END IF
      IF(VMS.LE.0.0) THEN
C
C        STREAMLINE PLOT
C
         NVEC=1
         LOC=1+N1*N2
C
C        SAVE WINDOW LIMITS SINCE KILDAT READJUSTS THEM AUTOMATICALLY
C
         XBSV=XBEG
         XESV=XEND
         YBSV=YBEG
         YESV=YEND
         CALL KILDAT(RBUF(  1),N1,N2,XBEG,XEND,YBEG,YEND,NPX,NPY,
     X               ISKPI,ISKPJ)
         CALL KILDAT(RBUF(LOC),N1,N2,XBSV,XESV,YBSV,YESV,NPX,NPY,
     X               ISKPI,ISKPJ)
         CALL SET(XL,XR,YB,YT,XBEG,XEND,YBEG,YEND,1)

C
C     IF GRID SPACING IN TWO DIRECTIONS IS NOT IDENTICAL, SCALE THE
C     COMPONENT THAT CORRESPONDS TO THE DIRECTION WITH THE COARSER
C     SPACING.
         IF (CSP(3,L1).GT.CSP(3,L2)) THEN
            DO I=1,(N1*N2)
               RBUF(I)=RBUF(I)*CSP(3,L2)/CSP(3,L1)
            END DO
         ELSE IF (CSP(3,L2).GT.CSP(3,L1)) THEN
            DO I=1,(N1*N2)
               RBUF(LOC+I-1)=RBUF(LOC+I-1)*CSP(3,L1)/CSP(3,L2)
            END DO
         END IF
         CALL GQPLCI(IERROR,IOLDCOL)
         IF (IERROR.NE.0) THEN
            WRITE(*,*)'***ERROR CALLING GQPLCI IN VECDSP***'
            CALL FLUSH_STDOUT
         END IF
         CALL SFLUSH
         IF (NVECCOL.GT.0) CALL GSPLCI(ICOLMAP(NVECCOL))
         CALL STRMLN(RBUF,RBUF(LOC),WBUF,N1,NPX,NPY,1,IVDENS,BAD,IER)
         CALL SFLUSH
         CALL GSPLCI(IOLDCOL)
      ELSE
C
C        VECTOR PLOT
C
         VKM=VECTIN(3)
         IAROW=VECTIN(4)
         HPRO=VECTIN(5)
         OMS=VKM/VMS
c         print *,'VECDSP: framit,vkm,vms,oms=',framit,vkm,vms,oms
         INC=IVDENS
         ILV=-1
         IF(INC.GT.0) GO TO 20
            ILV=IABS(INC+1)
            INC=1
   20    CONTINUE
         CALL SET(XL,XR,YB,YT,
     X          PLWIND(1,L1),PLWIND(2,L1),PLWIND(1,L2),PLWIND(2,L2),1)
C
C        DRAW U REFERENCE VECTOR
C
         MX=KUPX(PLWIND(2,L1)) + 125
         MY=KUPY(PLWIND(1,L2)) + 15

         DO 10 I=1,7
            VLEN=VLBUF(I)
            MV1=KUPX(CPUX(MX) - VLEN*OMS)
            IF (MV1.GE.KUPX(PLWIND(2,L1))) GOTO 15
   10    CONTINUE
   15    CONTINUE
         IF (VLEN.GE.1.0) THEN
            WRITE (JTIT,103)NINT(VLEN)
 103        FORMAT(I3,' M/S')
         ELSE
            WRITE(JTIT,174)VLEN
 174        FORMAT(F3.1,' M/S')
         END IF
         CALL GSCLIP(0)
         MV2=MX
         CALL PLCHMQ(CPUX(MV2),CPUY(MY-17),JTIT(1:7),12.,0.,
     X        1.)
         CALL LINE(CPUX(MV1),CPUY(MY),CPUX(MV2),CPUY(MY))
         CALL LINE(CPUX(MV1),CPUY(MY),CPUX(MV1+8),CPUY(MY-12))
         CALL LINE(CPUX(MV1),CPUY(MY),CPUX(MV1+8),CPUY(MY+8))
         CALL VECDRW(RBUF,N1,N2,CSP,PLWIND,L1,L2,INC,ILV,OMS,HPRO,
     X               IAROW,BAD,NVEC,NVECCOL,ISKPI,ISKPJ)
C
C     DRAW V REFERENCE VECTOR IF DIFFERENCE IN SCALING
C
         IF (AXSFAC(3).NE.1.0) THEN
            MY1=MY
            MY2=MY+(MV2-MV1)
            CALL LINE (CPUX(MV2),CPUY(MY1),CPUX(MV2),CPUY(MY2))
            CALL LINE (CPUX(MV2),CPUY(MY2),CPUX(MV2+8),CPUY(MY2-8))
            CALL LINE (CPUX(MV2),CPUY(MY2),CPUX(MV2-8),CPUY(MY2-8))
            VLEN=VLEN/AXSFAC(3)
            WRITE (JTIT,155)VLEN
 155        FORMAT(F4.1,' M/S')
            CALL PLCHMQ(CPUX(MV2+10),CPUY(MY1),JTIT(1:8),12.,90.,-1.)
         END IF
      END IF
C
      CALL GFLAS2
      IF(NVEC.NE.0) GO TO 30
C
C        NO VECTORS PLOTTED
C
         CALL GFLAS1(1)
         WRITE (JTIT,104)((NAME(I,J),I=1,4),J=1,2)
  104    FORMAT('NO ',4A2,',',4A2,' VECTORS AT THIS LEVEL')
         CALL PLCHMQ(CPUX(275),CPUY(LOCY),JTIT(1:42),12.,0.,-1.)
         CALL GFLAS2
   30 CONTINUE
      IF(ITYP.NE.1) GO TO 90
C
C        PRODUCE A COMPLETELY LABELED VECTOR PLOT
C
      CALL MAJMIN(XRANGE(1),DELRG(1),IFMTX,MAJORX,MINORX,NDIG1,ISZ1,
     X            YRANGE(1),DELRG(2),IFMTY,MAJORY,MINORY,NDIG2,ISZ2)
C      CALL MAJMIN(DELRG(2),IFMTY,MAJORY,MINORY)
      CALL SET(XL,XR,YB,YT,XRANGE(1),XRANGE(2),YRANGE(1),YRANGE(2),1)
      WRITE (CFMTX,510)IFMTX
 510  FORMAT(2A8)
      WRITE (CFMTY,510)IFMTY

C     Always draw grid and grid labels
C
      CALL LABMOD(CFMTX,CFMTY,NDIG1,NDIG2,ISZ1,ISZ2,4,4,0)
      CALL PERIML(MAJORX,MINORX,MAJORY,MINORY)

      CALL DATEE(NOW)
      CF=1./ID(69)
      XOR=ID(40)*CF
      SF=1./ID(68)
      XREL=ID(41)*SF
      YREL=ID(42)*SF
      WRITE (JTIT,106)(ID(I),I=116,121),(ID(I),I=125,127),
     X                    (ID(I),I=13,15),AXNAM(L3),RLEV,
     X                     LABAXS(L3,IUNAXS)
  106 FORMAT(I2.2,'/',I2.2,'/',I2.2,6X,I2.2,2(':',I2.2),'-',
     X       I2.2,2(':',I2.2),7X,3A2,7X,A2,'=',F7.2,' ',A4)
      IF (LABFLG.GT.5) THEN
         CALL PLCHMQ(CPUX(60),CPUY(1010),JTIT(1:66),12.,0.,-1.)
         WRITE (JTIT,107)NOW
 107     FORMAT('(AS OF ',A8,')')
         CALL PLCHMQ(CPUX(10),CPUY(985),JTIT(1:16),12.,0.,-1.)
         WRITE (JTIT,105)XREL,YREL,XOR
 105     FORMAT('ORIGIN=(',F7.2,',',F7.2,') KM   X-AXIS=',F5.1,' DEG')
         CALL PLCHMQ(CPUX(430),CPUY(985),JTIT(1:46),12.,0.,-1.)
         CALL PLCHMQ(CPUX(200),CPUY(960),CITIT,12.,0.,-1.)
         WRITE (JTIT,108)AXNAM(L2),LABAXS(L2,IUNAXS)
 108     FORMAT(A1,' ',A4)
      END IF
c      print *,'Vecdsp: axnam',axnam
      LOCY=LOCPLT(YB+(YT-YB)*0.15)
      IF (LABFLG.GT.5) THEN
         CALL PLCHMQ(CPUX(10),CPUY(LOCY),JTIT(1:6),12.,90.,-1.)
         WRITE (JTIT,108)AXNAM(L1),LABAXS(L1,IUNAXS)
      END IF
      LOCY=LOCPLT(YB)-50
      IF (LABFLG.GT.5) THEN
         CALL PLCHMQ(CPUX(150),CPUY(LOCY),JTIT(1:6),12.,0.,-1.)
      END IF
C
C        FLUSH VECTOR OVERLAY TO DEVICE
C        VMS .LT. 0 ==> No call frame (multiple vectors per plot)
C
      CALL GFLAS3(1)
      IF (LABFLG.GT.5) THEN
         IF(FRAMIT)CALL MYFRAME
      ELSE
         IF(FRAMIT)CALL FRAME
      END IF
   90 CONTINUE
C
C        NORMAL TERMINATION
C
      NST=0
      RETURN
      END
