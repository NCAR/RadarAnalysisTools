      SUBROUTINE SETCNT(NAMF,IFLDW,IDEFMD,SCLFAC,THKLIN,KRD,NST)
C
C        ESTABLISHES CONTOUR LEVELS FOR EVERY FIELD IN THE EDIT FILE
C                    ALSO SETS CONTOUR PLOT CHARACTERISTICS.
C

      INCLUDE 'CEDRIC.INC'      
      PARAMETER (MAXLEV=61,NPLTYPS=15)
      COMMON /CONTUR/ CLB(MAXLEV,NFMAX),NLB(NFMAX),ICSPCS(10,NFMAX)
      COMMON /SETWND/ ISETW(2,3),PSETW(2,3),ISETFL(NFMAX)
      DIMENSION SCLFAC(NFMAX),CLIN(MAXLEV),IFLDW(NFMAX),NLMAX(NPLTYPS)
      DIMENSION THKLIN(NFMAX)
      CHARACTER*(*) KRD(10)
      CHARACTER*1 CIBL,ICLTYP
      CHARACTER*2 NAMF(4,NFMAX),NAME(4)
      CHARACTER*3 IAPS(3),ITEST
      CHARACTER*8 CTEMP,IPTS(NPLTYPS),IPTYP,NAMIN,IPLOPS,IDFPLP
      DATA IAPS/'PRI','SEC','ALL'/
      DATA IPTS/'CONT    ','HAFT    ','CHAFT   ','FALL',
     X          'FHOT','FCOLD', 'CFALL', 'CFHOT', 'CFCOLD', 'GREYS', 
     X          'CGREYS','CGREYS2','CHAFT2','CGREYS3','CGREYS4'/
      DATA IDFPLP/'00101000'/
      DATA CIBL/' '/
      DATA NLMAX/61,61,61,61,31,31,61,31,31,61,61,61,61,61,61/
C
C        DETERMINE FIELD PLOTTING ACTIVITY TO BE IMPLEMENTED
C
      NST=0
      READ (KRD,101)NAMIN,IPTYP,IPLOPS,SCL,THK,ICLTYP,(CLIN(I),I=1,3)
C  101 FORMAT(16X,A8,A1,7X,A8,F8.0,A1,7X,3F8.0)
 101  FORMAT(//A8/A8/A8/F4.0,F4.0/A1/F8.0/F8.0/F8.0)
      IF (IPTYP.EQ.'BOTH') IPTYP='CHAFT'
      ICHB=IFINDC(IPTYP,IPTS,NPLTYPS,0)

C
C     TAG THE HALFTONE PLOTS FOR FOREGROUND/BACKGROUND COLOR CHANGES
C
      IF ((ICHB.EQ.10 .OR. ICHB.EQ.11 .OR. ICHB.EQ.12 .OR.
     X     ICHB.EQ.14 .OR. ICHB.EQ.15) .AND. IDEFMD.GT.0) THEN
         IDEFMD=2
      ELSE IF ((ICHB.EQ.10 .OR. ICHB.EQ.11 .OR. ICHB.EQ.12 .OR.
     X          ICHB.EQ.14 .OR. ICHB.EQ.15) .AND. IDEFMD.LT.1) THEN
         IDEFMD=-2
      END IF
      ICHB=MAX0(ICHB,1)
      IF(IPLOPS.EQ.CIBL) IPLOPS=IDFPLP
      IF(ICLTYP.NE.'N') THEN
C
C        UNIFORM CONTOURS
C
         IF(CLIN(3).LE.0.0) CLIN(3)=10.0
         IF(CLIN(1).EQ.0.0.AND.CLIN(2).EQ.0.0) CLIN(2)=60.0
         NL=(CLIN(2)-CLIN(1))/CLIN(3) + 1.00001
         IF(NL.LE.0.OR.NL.GT.NLMAX(ICHB)) GO TO 91
         RINC=CLIN(3)
         IF(NL.GE.2) THEN
            DO 5 I=2,NL
               CLIN(I)=CLIN(I-1)+RINC
    5       CONTINUE
         END IF
      ELSE
C
C        NON-UNIFORM CONTOURS
C
         NL=0
    8    CONTINUE
         CALL KARDIN(KRD)
         DO 10 K=2,10
            IF(KRD(K).EQ.CIBL) GO TO 11
            NL=NL+1
            READ (KRD(K),102)CLIN(NL)
  102       FORMAT(F8.0)
            IF (NL.GT.1 .AND. CLIN(NL).LT.CLIN(NL-1)) GOTO 93
   10    CONTINUE
         IF(NL.LE.NLMAX(ICHB)) GO TO 8
 11      CONTINUE
         IF(NL.GT.NLMAX(ICHB)) GO TO 91
      END IF
C
C        DETERMINE FIELD(S) TO BE PARAMETERIZED
C
      WRITE (CTEMP,100)NAMIN
      READ (CTEMP,103)ITEST
  103 FORMAT(A3)
      LF=IFINDC(ITEST,IAPS,3,0)
      IF(NAMIN.EQ.CIBL) LF=1
      IF(LF.EQ.0) GO TO 15
C
C        GROUP OF FIELDS
C
      IF(LF.EQ.3) THEN
         J=IFINOT(0,ISETFL,NFMAX,0)
      ELSE
         J=IFIND(LF,ISETFL,NFMAX,0)
      END IF
      IF(J.EQ.0) GO TO 90
      GO TO 20
   15 CONTINUE
C
C        INDIVIDUAL FIELD
C
      WRITE (CTEMP,100)NAMIN
      READ (CTEMP,104)NAME
  104 FORMAT(4A2)
      J=LOCFLD(NAME,NAMF,4,NFMAX,4)
      IF(J.EQ.0) GO TO 92
   20 CONTINUE
C
C        ACTIVATE FIELD(S) TO BE CONTOURED AND
C        SAVE CONTOURING INFO IN THE APPROPRIATE DATA STRUCTURE
C
      CALL FLDSET(NAMIN,NAMF,IFLDW,IDEFMD,NST)
      IF(NST.NE.0) GO TO 90
      SCLFAC(J)=SCL
      THKLIN(J)=THK
      IF (THKLIN(J).LE.0.0) THKLIN(J)=1.0
      NLB(J)=NL
      DO 25 I=1,NL
         CLB(I,J)=CLIN(I)
   25 CONTINUE
      WRITE (CTEMP,100)IPLOPS
 100  FORMAT(A8)
      READ (CTEMP,105)(ICSPCS(I,J),I=1,8)
  105 FORMAT(8I1)
      ICSPCS(9,J)=ICHB
      ICSPCS(10,J)=0
C
C        WRAP UP HERE- SET ALL FIELDS IF LF NON-ZERO
C
      IF(LF.EQ.0) GO TO 90
         DO 55 L=1,NFMAX
            IF(ISETFL(L).EQ.0) GO TO 55
            IF(LF.NE.3.AND.LF.NE.ISETFL(L)) GO TO 55
            SCLFAC(L)=SCLFAC(J)
            THKLIN(L)=THKLIN(J)
            NLB(L)=NLB(J)
            DO 51 I=1,NL
               CLB(I,L)=CLB(I,J)
   51       CONTINUE
            DO 52 I=1,10
               ICSPCS(I,L)=ICSPCS(I,J)
   52       CONTINUE
   55    CONTINUE
   90 CONTINUE
      RETURN
   91 CONTINUE
      NST=1
      CALL CEDERX(542,0)
      RETURN
   92 CONTINUE
      NST=1
      CALL CEDERX(507,0)
      RETURN
 93   CONTINUE
      NST=1
      CALL CEDERX(596,0)
      RETURN
      END
