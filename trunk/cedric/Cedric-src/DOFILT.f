      SUBROUTINE DOFILT(OBUF,TBUF,RBUF,NX,NY,BAD,WEIGHT,IS,IF,JS,JF,IP)
C
C        DOES THE ACTUAL FILTERING
C
C        OBUF - SCRATCH BUFFER DIMENSIONED (NX,NY)
C        TBUF - SCRATCH BUFFER DIMENSIONED (NX,NY)
C        RBUF - INPUT/OUTPUT BUFFER DIMENSIONED (NX,NY)
C        NX - TOTAL WINDOW DIMENSION
C        NY - TOTAL WINDOW DIMENSION
C        BAD - SET UNFILTERABLE POINTS TO BAD
C        WEIGHT - WEIGHTS OF POINTS FOR FILTERING DIMENSIONED (3)
C        IP - TYPE OF FILTER DESIRED  1-3=(3 X 3) WEIGHTED
C                                     4=LEISE'S
C                                     5=LEAST SQUARE
C                                     6=TWO-WAY HANNING
C
      DIMENSION OBUF(NX,NY),RBUF(NX,NY),TBUF(NX,NY),WEIGHT(3)
C
      NPLANE=NX*NY
      CALL COPRX(OBUF,RBUF,NPLANE)
      CALL EXTEND(OBUF,NX,NY,1,BAD)
      IF(IP.LE.3) GO TO 120
      IF (IP.EQ.4 .OR. IP.EQ.9) THEN
C
C        LEISE'S FILTER
C
      NSTEP=WEIGHT(1)
      CALL T5FLTR(OBUF,NX,NY,1,NSTEP)
      DO 60 J=JS,JF
         DO 50 I=IS,IF
            IF(RBUF(I,J).EQ.BAD.OR.OBUF(I,J).EQ.BAD) GO TO 50
            RBUF(I,J)=OBUF(I,J)
 50      CONTINUE
 60   CONTINUE
C
      ELSE IF(IP.EQ.5) THEN
C
C        LEAST SQUARES FILTER
C
      ITMAX=WEIGHT(1)
      CALL LSQFIL(RBUF,OBUF,NX,NY,IS,IF,JS,JF,ITMAX,BAD)
C
      ELSE IF(IP.EQ.6) THEN
C
C        TWO-WAY HANNING  --DO FORWARD FILTER
C
      IF(OBUF(1,1).EQ.BAD) GO TO 135
      WEIGHT(1)=0.25
      WEIGHT(2)=0.125
      WEIGHT(3)=0.0625
      NYM1=NY-1
      NXM1=NX-1
      CALL COPRX(TBUF,OBUF,NPLANE)
      DO 115 J=2,NYM1
         JL=J-1
         JR=J+1
         DO 112 I=2,NXM1
            IL=I-1
            IR=I+1
            OBUF(I,J)=  WEIGHT(1)*TBUF(I,J) +
     X WEIGHT(2)*(TBUF(IL,J)+TBUF(I,JR)+TBUF(IR,J)+TBUF(I,JL)) +
     X WEIGHT(3)*(TBUF(IL,JL)+TBUF(IL,JR)+TBUF(IR,JR)+TBUF(IR,JL))
 112     CONTINUE
 115  CONTINUE
C
C        INVERSE HANNING WEIGHTS  --BRANCH TO FILTERING
C
      WEIGHT(1)=2.89
      WEIGHT(2)=-0.595
      WEIGHT(3)=0.1225
C
      GO TO 120
C
      END IF
C
      RETURN
C
C        NORMAL FILTERING
C
 120  CONTINUE
      DO 130 J=JS,JF
         JL=J-1
         JR=J+1
         IF (JL.LT.1 .OR. JR.GT.NY) GO TO 130
         DO 128 I=IS,IF
            IF(RBUF(I,J).EQ.BAD) GO TO 128
            IL=I-1
            IR=I+1
         IF (IL.LT.1 .OR. IR.GT.NX) GO TO 128
            DO 125 II=IL,IR
               DO 124 JJ=JL,JR
                  IF (OBUF(II,JJ).EQ.BAD) GO TO 128
 124           CONTINUE
 125        CONTINUE
            RBUF(I,J)=  WEIGHT(1)*OBUF(I,J) +
     X WEIGHT(2)*(OBUF(IL,J)+OBUF(I,JR)+OBUF(IR,J)+OBUF(I,JL)) +
     X WEIGHT(3)*(OBUF(IL,JL)+OBUF(IL,JR)+OBUF(IR,JR)+OBUF(IR,JL))
 128     CONTINUE
 130  CONTINUE
 135  CONTINUE
      RETURN
      END
