      SUBROUTINE CORCAL(C,A,B,N1,N2,IS,IF,JS,JF,IC,JC,LAGX,LAGY,BAD)
C
C
C        CALCULATES THE CROSS-CORRELATION BETWEEN TWO (2-D) FIELDS
C
C           C- CROSS-CORRELATION OUTPUT FIELD
C           A- REFERENCE INPUT FIELD
C           B- LAG INPUT FIELD
C          N1- I-DIMENSION OF FIELDS
C          N2- J-DIMENSION OF FIELDS
C          IS- STARTING I-INDEX
C          IF-   ENDING I-INDEX
C          JS- STARTING J-INDEX
C          JF-   ENDING J-INDEX
C          IC- I-INDEX OF ZERO LAG IN OUTPUT FIELD
C          JC- J-INDEX OF ZERO LAG IN OUTPUT FIELD
C        LAGX- (+/-) LAG ALONG I
C        LAGY- (+/-) LAG ALONG J
C         BAD- MISSING DATA FLAG
C
C
      DIMENSION C(N1,N2),A(N1,N2),B(N1,N2)
      DATA CNTMIN /2.0/
      DATA EPS /1.E-8/
C
C        CALCULATE INDICES OF THE OUTPUT FIELD
C
      LJ1=JC-LAGY
      LJ2=JC+LAGY
      LI1=IC-LAGX
      LI2=IC+LAGX
C
C        LOOP FOR EACH LAG CORRELATION
C
      DO 30 JP=LJ1,LJ2
         LJ=JP-JC
         JA1=MAX0(JS,JS+LJ)
         JA2=MIN0(JF,JF+LJ)
         JBINIT=MAX0(JS,JS-LJ)
      DO 25 IP=LI1,LI2
         LI=IP-IC
         IA1=MAX0(IS,IS+LI)
         IA2=MIN0(IF,IF+LI)
         IBINIT=MAX0(IS,IS-LI)
C
C        ZERO OUT CORRELATION CALCULATION ACCUMULATORS
C
         CNT=0.0
         PAB=0.0
         SUMA=0.0
         SUMB=0.0
         SUMSQA=0.0
         SUMSQB=0.0
C
C        LOOP FOR ALL (I,J) LOCATIONS IN THE OVERLAPPING REGION
C
         JB=JBINIT
         DO 15 JA=JA1,JA2
         IB=IBINIT
         DO 10 IA=IA1,IA2
C
C           VECTORIZED CODE
C
            OK=CVMGT(1.0,0.0,A(IA,JA).NE.BAD.AND.B(IB,JB).NE.BAD)
            CNT=CNT+OK
            PAB=PAB+A(IA,JA)*B(IB,JB)*OK
            SUMA=SUMA+A(IA,JA)*OK
            SUMB=SUMB+B(IB,JB)*OK
            SUMSQA=SUMSQA+A(IA,JA)*A(IA,JA)*OK
            SUMSQB=SUMSQB+B(IB,JB)*B(IB,JB)*OK
         IB=IB+1
   10    CONTINUE
         JB=JB+1
   15    CONTINUE
         C(IP,JP)=BAD
         IF(CNT.LT.CNTMIN) GO TO 25
C
C        COMPUTE CORRELATION COEFFICIENT FOR THIS LAG
C
         CNTI=1./CNT
         SUMA=SUMA*CNTI
         SUMB=SUMB*CNTI
         T1=SUMSQA*CNTI-SUMA*SUMA
         T2=SUMSQB*CNTI-SUMB*SUMB
         IF(T1.LT.EPS.OR.T2.LT.EPS) GO TO 25
         DENO=SQRT(T1)*SQRT(T2)
         IF(DENO.LT.EPS) GO TO 25
         C(IP,JP)=(PAB*CNTI-SUMA*SUMB)/DENO
   25 CONTINUE
   30 CONTINUE
      RETURN
      END
