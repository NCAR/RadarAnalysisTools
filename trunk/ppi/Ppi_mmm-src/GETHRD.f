c
c----------------------------------------------------------------------X
c
      SUBROUTINE GETHRD(IUN,AZ,EL,ITIME,IDATE,IYR,IMON,IDAY,R0,DR,NGTS,
     X                  RADCON,IEOS,IEOT,BDVAL,IBTIME,DAT,IFL,NAZ,RCOR,
     X                  MXR,MXA,MXF,DEC,DECWR,WORDSZ)

C     ROUTINE TO READ A RAY OF HRD DATA.  SEVERAL RAYS OF DATA ARE
C     PACKED INTO A PHYSICAL RECORD AND THE DATA ARE COMPRESSED WITH
C     STRINGS OF DATA BELOW NOISE POWER REMOVED AND REPLACED BY TWO
C     DATA BYTES: A 0 BYTE FOLLOWED BY THE NUMBER OF RANGE GATES BELOW
C     THE NOISE POWER. THE MAXIMUM RECORD SIZE IS 4096 BYTES.
C
C     DEC   - (1) Reading input on DEC,     (0) Reading input on non-DEC
C     DECWR - (1) Input was written on DEC, (0) Input was written on non-DEC
C
C     REF-    ARRAY TO HOLD THE DBM VALUES
C     DBM-    LOOKUP TABLE TO CONVERT THE INTERGER VALUES ON TAPE TO
C             DBM VALUES.  DBM IS SET IN SUBROUTINE CALIB.
C     IBUF-   DATA BUFFER TO HOLD THE PACKED INPUT DATA.
C     ID-     DATA BUFFER TO HOLD THE UNPACKED INPUT DATA BEFORE IT HAS
C             BEEN DECOMPRESSED.
C     IRAY-   DATA BUFFER TO HOLD THE UNPACKED INPUT DATA AFTER IT HAS
C             BEEN DECOMPRESSED.  DURING THE DECOMPRESSION PROCESS, STRINGS
C             OF 0'S ARE REPLACED BY THE BAD DATA FLAG.



      DIMENSION DAT(MXR,MXA,MXF),DBM(256),IBUF(1250),RCOR(MXR)
      DIMENSION ID(5000),IRAY(2024)

      DATA IRD,IFIR/1,1/
      DATA ICAL/1/
      DATA NEOF/0/

C IF THIS IS THE FIRST READ, CALCULATE THE RADAR CONSTANT AND THE
C LOOKUP TABLE TO CONVERT THE BYTE VALUES ON TAPE TO DBM VALUES.

      IF(ICAL.EQ.1)THEN
         CALL CALIB(DBM,RADCON)

         ICAL=0
      END IF

C     READ A PHYSICAL RECORD(IRD=1). A PHYSICAL RECORD CONTAINS EITHER A SCAN
C     HEADER OR DBM DATA.  DBM DATA ARE PACKED SUCH THAT THERE WILL BE MULTIPLE
C     LOGICAL RECORDS (BEAMS) PER PHYSICAL RECORD.  IF ALL OF THE BEAMS HAVE
C     NOT YET BEEN UNPACKED FROM THE CURRENT PHYSICAL RECORD, DO NOT READ A
C     A NEW ONE.

 10   IF(IRD.EQ.1)THEN
         IPT=2
         IRD=0
         NRAYS=0
CANNE        BUFFER IN(IUN,1) (IBUF(1),IBUF(625))
CANNE        RST=UNIT(IUN)
         CALL RDCOSREC(IBUF,NWDS)

         IF(NWDS.LE.0.)THEN
            NEOF=NEOF+1
            WRITE(*,*)'END OF FILE'
            IF(NEOF.EQ.2)THEN
               IEOT=1
               WRITE(*,*)'END OF TAPE'
               RETURN
            END IF
            GO TO 10
         END IF
         NEOF=0
CANNE        IF(RST.EQ.-1.0)N64=LENGTH(IUN)
         NBYTES=NINT(WORDSZ/8)

         N8=NWDS*8

C     BYTE-SWAPPING CHECK
C
         IF((DEC .EQ. 1.0 .AND. DECWR .EQ. 0.0).OR.
     +      (DEC .EQ. 0.0 .AND. DECWR .EQ. 1.0))THEN
            CALL SWAP32(IBUF,NWDS*2)
         END IF

C        UNPACK THE FIRST WORD OF THE PHYSICAL RECORD.  IF THE FIRST
C        WORD IS -1, THEN THE RECORD IS A SCAN HEADER.

         CALL GBYTES(IBUF(1),IWRD1,0,16,0,1)
         IF(IWRD1.GT.32767)IWRD1=IWRD1-65536

      END IF

C THE CURRENT RECORD IS A SCAN HEADER-GO UNPACK THE HEADER INFORMATION

      IF(IWRD1.EQ.-1)THEN

C        IF A SCAN HAS ALREADY BEEN READ (IFIR=0) PRINT OUT THE HEADER
C        INFORMATION FOR THAT SCAN AND RETURN TO THE MAIN PROGRAM TO DO THE
C        PROCESSING-PLOTTING.  THE SCAN HEADER FOR THE NEW SCAN WILL BE
C        UNPACK BELOW WHEN THE PROCESSING IS DONE AND THIS ROUTINE IS
C        CALLED AGAIN.

         IF(IFIR.NE.1)THEN
            IEOS=1
            IFIR=1
c            PRINT 25,IEOS,NAZ,IDATE,ITIME,NGTS,EL,DR,R0
c 25         FORMAT(1X,'IEOS,NAZ,DATE,TIME,NGTS,EL,DR,R0=',4I7,I5,
c     +      F6.1,F5.1,F5.2)
c            NAZ=0
            RETURN
         END IF

C        UNPACK THE SCAN HEADER AND SET THE READ RECORD FLAG (IRD=1) SO THAT
C        THE NEXT RECORD CONTAINING THE DBM DATA WILL BE READ.

         CALL RDHDR(IBUF,IDATE,IYR,IMON,IDAY,ITIME,NGTS,IBEG,IEND,DR,R0)
         IRD=1
         IF(ITIME.LT.IBTIME)GO TO 10
         IFIR=0
         GO TO 10
      END IF

      IF(ITIME.LT.IBTIME)THEN
         IRD=1
         GO TO 10
      END IF

C     IF THE GATE SPACING OR RANGE TO FIRST GATE CHANGES, UPDATE
C     THE RANGE AND 20*LOG(RANGE) ARRAYS.


C     UNPACK THE 64-BIT WORDS INTO 8-BIT VALUES.

      CALL GBYTES(IBUF(1),ID(1),0,8,0,N8)

C     SWAP THE BYTES TO GET DATA IN CORRECT ORDER

      DO 50 I=1,N8-1,2
         ITMP=ID(I)
         ID(I)=ID(I+1)
 50      ID(I+1)=ITMP
 55   FORMAT(1X,'ID= ',20I4)

C     NRADS=THE NUMBER OF BEAMS PACKED INTO THE CURRENT PHYSICAL RECORD

      NRADS=ID(1)

C DECOMPRESS THE DATA. PUT RESULTS IN IRAY

      CALL UNPCK(ID,N8,IPT,NGTS,IBEG,IEND,IRAY,AZ,EL)

C CONVERT BYTE TAPE VALUES TO DBM

      DO 70 I=1,NGTS
         IZ=IRAY(I)
         DAT(I,NAZ,IFL)=BDVAL
70       IF(IZ.GT.0)DAT(I,NAZ,IFL)=DBM(IZ)+RADCON+RCOR(I)


C     INCREMENT THE NUMBER OF RAYS THAT HAVE BEEN UNPACKED FROM THE
C     CURRENT PHYSICAL RECORD.  IF THE NUMBER EQUALS OR EXCEEDS THE
C     TOTAL NUMBER PACKED IN THE RECORD, SET THE READ RECORD FLAG TO
C     1 SO THAT ANOTHER PHYSICAL RECORD WILL BE READ.

      NRAYS=NRAYS+1

      IF(NRAYS.GE.NRADS)IRD=1
      RETURN
      END
