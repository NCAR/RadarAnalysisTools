c
c----------------------------------------------------------------------X
c
      SUBROUTINE SAVVAD(INDAT,VADTYPE,NAMPLVD,JVD,ZMNVD,ZMXVD,ISKPVD,
     X     U_VD,V_VD,AZMVD,WFILT,NVD,XMNVD,XMXVD,XSCLVD,XREFVD,TYPVD,
     X     NVDMX,MXVD)
C
C  READ PLTVAD STACK FOR VAD OUTPUT SCATTERGRAM CHARACTERISTICS
C
C     VADTYPE     - TYPE OF VAD ANALYSIS (VAD or COV) TO BE PLOTTED
C     NAMPLVD     - NAME OF VAD ANALYSIS               "  "    "
C     JVD         - NUMBER OF VAD ANALYSES             "  "    "
C     NVD         - NUMBER OF SCATTERGRAMS             "  "    "
C     ZMNVD,ZMXVD - MINIMUM AND MAXIMUM HEIGHT (KM)    "  "    "
C     ISKPVD      - HEIGHT SKIPPING FACTOR
C     U_VD        - AMOUNT TO SUBTRACT FROM U COMPONENT
C     V_VD        -    "    "     "      "  V     "
C     AZMVD       - AZIMUTH ANGLE OF +U-COMPONENT OF WINDS
C     WFILT       - WIDTH OF VERTICAL FILTER (KM)
C     XMNVD,XMXVD - MINIMUM AND MAXIMUM VALUE OF THE ABSCISSA FIELD
C     XREFVD      - REFERENCE VALUE
C     XSCLVD      - SCALING FACTOR (PLOT VALUE*XSCL)
C     TYPVD       - TYPE OF PLOT: 'SCAT', 'LINE', OR 'BOTH'
C
      CHARACTER*8 INDAT(10),JNDAT(10),NAMFLD,NAMPLVD(MXVD)
      CHARACTER*4 TYPVD(NVDMX,MXVD),VADTYPE(MXVD)
      DIMENSION XMNVD(NVDMX,MXVD),XMXVD(NVDMX,MXVD)
      DIMENSION XSCLVD(NVDMX,MXVD),XREFVD(NVDMX,MXVD)
      DIMENSION ZMNVD(MXVD),ZMXVD(MXVD),ISKPVD(MXVD)
      DIMENSION U_VD(MXVD),V_VD(MXVD),AZMVD(MXVD),WFILT(MXVD)

      JVD=JVD+1
      WRITE(6,11)(INDAT(I),I=2,10)
   11 FORMAT(1X,'PVAD : ',9A8)
      READ(INDAT,13)VADTYPE(JVD),NAMPLVD(JVD),ZMNVD(JVD),ZMXVD(JVD),
     +     ZSKIP,U_VD(JVD),V_VD(JVD),AZMVD(JVD),WFILT(JVD)
   13 FORMAT(/A4,4X/A8/F8.0/F8.0/F8.0/F8.0/F8.0/F8.0/F8.0)
      IF(ZSKIP.LE.0.0)THEN
         ISKPVD(JVD)=1
      ELSE
         ISKPVD(JVD)=ZSKIP
      END IF

C     READ FIELD NAMES, MIN VALUES, MAX VALUES UNTIL END-OF-STACK
C
      NVD=0
   20 READ(5,21)(JNDAT(I),I=1,10)
   21 FORMAT(10A8)
c      WRITE(6,22)(JNDAT(I),I=1,10)
c   22 FORMAT('Kardin=',10A8)
      IF(JNDAT(1)(1:1).EQ.'*')GO TO 20
      IF(JNDAT(1).EQ.'END     ')RETURN
      IF(JNDAT(1).NE.'        ')THEN
         WRITE(6,23)
   23    FORMAT(1X,'*** SAVVAD: NO END LINE ENCOUNTERED ***')
         STOP
      END IF
      NVD=NVD+1
      IF(NVD.GT.NVDMX)THEN
         WRITE(6,25)NVDMX
   25    FORMAT(1X,'*** SAVVAD: NO. PLOTS EXCEEDS',I3,' - STOP ***')
         STOP
      END IF
      READ(JNDAT,27)NAMFLD,XMNVD(NVD,JVD),XMXVD(NVD,JVD),
     +              XREFVD(NVD,JVD),XSCLVD(NVD,JVD),TYPVD(NVD,JVD)
   27 FORMAT(/A8/F8.0/F8.0/F8.0/F8.0/A4,4X)
      IF(XSCLVD(NVD,JVD).EQ.0.0)XSCLVD(NVD,JVD)=1.0
      IF(TYPVD(NVD,JVD).NE.'SCAT' .AND. TYPVD(NVD,JVD).NE.'LINE' .AND.
     +   TYPVD(NVD,JVD).NE.'BOTH')TYPVD(NVD,JVD)='BOTH'
      WRITE(6,29)NVD,NAMFLD,XMNVD(NVD,JVD),XMXVD(NVD,JVD),
     +           XREFVD(NVD,JVD),XSCLVD(NVD,JVD),TYPVD(NVD,JVD)
   29 FORMAT(8X,'#',I2,' X AXIS: NAM,FMN-MX,REF,SCL= ',A8,4F8.2,4X,A4)
      GO TO 20
      END
