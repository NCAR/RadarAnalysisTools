c
c----------------------------------------------------------------------X
c
      SUBROUTINE R_SHFT(DAT,IOUT,IIN1,C1,BDVAL,MNGATE,MXGATE,NANG,
     X     GSPC,TMP1,TMP2,MXR,MXA,MXF)
C
C  FUNCTION - RSHIFT: F(R,OUT)=F(R-C1,IN); I.E. SHIFT THE INPUT FIELD
C             BY C1 (KM) IN RANGE TO ACCOUNT FOR FIELDS WITH DIFFERENT 
C             R0.  FOR EXAMPLE, FIND X-BAND REFLECTIVITIES AT S-BAND
C             RANGES WHERE S- AND X-BAND HAVE INITIAL RANGES R0 AND R0+C1. 
C             NOTE: ROUTINES TO INPUT DATA ASSUME THAT ALL FIELDS ARE AT 
C             IDENTICAL RANGES, WITH ONLY ONE RANGE FIELD THROUGHOUT THE
C             PROGRAM.  THIS ROUTINE ASSUMES EQUAL GATE SPACINGS AND 
C             EQUAL NUMBERS OF GATES AND BEAMS.
C
C     IOUT   - OUTPUT FIELD NUMBER
C     IIN1   -  INPUT   "      "
C     R0     - ORIGINAL INITIAL RANGE (KM, NOT USED AS INPUT)
C     GSPC   - GATE SPACING (KM)
C     C1     - TOTAL DISTANCE (KM) TO SHIFT THE OUTPUT
C              (C1.GT.0) - SHIFT ORIGINAL DATA RIGHT
C              (C1.LT.0) -   "      "       "  LEFT
C     TSHIFT - TOTAL DISTANCE FOR DATA TO BE SHIFTED (SET TO C1)
C     FSHIFT - FLOATING POINT SHIFT (TSHIFT SCALED BY GSPC) CONSISTING OF AN
C              INTEGER SHIFT AND A FRACTIONAL PART
C     FRAC   - FRACTIONAL PART OF THE SHIFT
C              
C
      DIMENSION TMP1(MXR),TMP2(MXR,MXA)
      DIMENSION DAT(MXR,MXA,MXF)

      TSHIFT=C1
      FSHIFT=TSHIFT/GSPC

      DO 100 J=1,NANG

C        SHIFT INPUT DATA, INCLUDING INTERPOLATION FOR INTEGER PLUST
C        FRACTION OF AN INTEGER SHIFT, I.E. TSHIFT IS NOT AN INTEGRAL 
C        NUMBER OF GATES.
C
          DO 80 I=MNGATE,MXGATE
            TMP1(I)=BDVAL
            FIL =FLOAT(I)-FSHIFT
            IL  =INT(FIL)
            IR  =IL+1
            FRAC=FIL-FLOAT(IL)

            IF(IL.LT.MNGATE.OR.IL.GT.MXGATE)GO TO 80
            IF(IR.LT.MNGATE.OR.IR.GT.MXGATE)GO TO 80
            DATL=DAT(IL,J,IIN1)
            DATR=DAT(IR,J,IIN1)
            IF(DATL.NE.BDVAL.AND.DATR.NE.BDVAL)THEN
               DATDIF=DATR-DATL
               TMP1(I)=DATL+DATDIF*FRAC
            END IF

 80      CONTINUE

C        TRANSFER SHIFTED DATA INTO OUTPUT ARRAY
C
         DO 90 I=MNGATE,MXGATE
            DAT(I,J,IOUT)=TMP1(I)
 90      CONTINUE

 100  CONTINUE

      RETURN
      END
