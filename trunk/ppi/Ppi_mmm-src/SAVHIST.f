c
c----------------------------------------------------------------------X
c
      SUBROUTINE SAVHIST(INDAT,NAMFLD,IFLD,NFLDS,NAH,IHNAM,HRMN,HRMX,
     X                   HAMN,HAMX,HZMN,HZMX,IHKP,JHKP,FMN,FMX,FBIN,
     X                   FREF,PMN,PMX,HTYP,HFIT,HMATCH,NHMX,HSTCOLR)
C
C  CONTINUE READING PLTHIST STACK FOR FIELDS TO BE PLOTTED
C
C     NAH       - NUMBER OF HISTOGRAMS TO BE PLOTTED
C     IHNAM     - NAMES   "   FIELDS    "  "    "
C     HRMN,HRMX - MINIMUM AND MAXIMUM RANGE  (KM) FOR ACCUMULATING HISTOGRAM
C     HAMN,HAMX -    "     "     "    ANGLE (DEG)  "        "          "
C     HZMN,HZMX -    "     "     "    HEIGHT (KM)  "        "          "
C     IHKP,JHKP - SKIPPING FACTORS FOR RANGE AND ANGLE
C     HMATCH    - Match angular skipping factor to range skipping factor
C     HSTCOLR   - Color to be used for histogram reference lines
C     FMN,FMX   - MINIMUM AND MAXIMUM FIELD VALUES FOR PLOT ABSCISSA (X)
C     FREF      - ABSCISSA (X) REFERENCE LINE (ALLOWS 2)
C     FBIN      - BIN WIDTH FOR ACCUMULATION OF PERCENT OCCURRENCE
C     PMN,PMX   - MINIMUM AND MAXIMUM PERCENT OCCURRENCE FOR PLOT ORDINATE (Y)
C     HTYP      - TYPE OF SCALING FOR ORDINATE (LINEAR OR LOGARITHMIC)
C     HFIT      - TYPE OF FIT TO DISTRIBUTION
C     NANG      - NUMBER OF ANGLES IN THE CURRENT SWEEP
C
      INCLUDE 'dim.inc'

      CHARACTER*8 INDAT(10),JNDAT(10),BLANK
      CHARACTER*8 NAMFLD(MXF),IHNAM(NHMX)
      CHARACTER*4 HTYP(NHMX),HFIT(NHMX)
      CHARACTER*4 NAMOUT
      CHARACTER*4 HMATCH
      CHARACTER*2 HSTCOLR
      DATA NAMOUT/'    '/
      DIMENSION IFLD(MXF)
      DIMENSION FMN(NHMX),FMX(NHMX),FBIN(NHMX),FREF(NHMX,2)
      DIMENSION PMN(NHMX),PMX(NHMX)
      DATA BLANK/'        '/

      NAH=0
      HMATCH='    '
      WRITE(6,11)(INDAT(I),I=2,10)
   11 FORMAT(1X,'PHIST: ',9A8)
      READ(INDAT,13)HRMN,HRMX,HISKIP,HAMN,HAMX,HJSKIP,HZMN,HZMX,HMATCH,
     X     HSTCOLR
   13 FORMAT(/F8.0/F8.0/F8.0/F8.0/F8.0/F8.0/F8.0/F8.0/A4,A2,2X)
      IF(HSTCOLR.EQ.'  ')HSTCOLR='WW'
      IF(HMATCH.NE.'YES')HMATCH='NO'
      IF(HISKIP.LE.0.0)THEN
         IHKP=1
      ELSE
         IHKP=HISKIP
      END IF
      IF(HJSKIP.LE.0.0)THEN
         JHKP=1
      ELSE
         JHKP=HJSKIP
      END IF
      IF(HZMN.LE.0.0 .AND. HZMX.LE.0.0)THEN
         HZMN=-999.0
         HZMX= 999.0
      END IF

C     READ FIELD NAME, MIN VALUE, MAX VALUE UNTIL END-OF-STACK
C
   20 READ(5,21)(JNDAT(I),I=1,10)
   21 FORMAT(10A8)
c      WRITE(6,22)(JNDAT(I),I=1,10)
c   22 FORMAT('Kardin=',10A8)
      IF(JNDAT(1)(1:1).EQ.'*')GO TO 20
      IF(JNDAT(1).EQ.'END     ')RETURN
      IF(JNDAT(1).NE.BLANK)THEN
         WRITE(6,23)
   23    FORMAT(1X,'*** SAVHIST: NO END LINE ENCOUNTERED ***')
         STOP
      END IF
      NAH=NAH+1
      IF(NAH.GT.NHMX)THEN
         WRITE(6,25)NHMX
   25    FORMAT(1X,'*** SAVHIST: NO. PLOTS EXCEEDS',I3,' - STOP ***')
         STOP
      END IF
      READ(JNDAT,27)IHNAM(NAH),FMN(NAH),FMX(NAH),FBIN(NAH),
     +     PMN(NAH),PMX(NAH),HTYP(NAH),HFIT(NAH),FREF(NAH,1),
     +     FREF(NAH,2)
   27 FORMAT(/A8/F8.0/F8.0/F8.0/F8.0/F8.0/A4,A4/F8.0/F8.0)
      IF(JNDAT( 9).EQ.BLANK)FREF(NAH,1)=99.9
      IF(JNDAT(10).EQ.BLANK)FREF(NAH,2)=99.9
C
      CALL FIELD(IHNAM(NAH),NAMFLD,NFLDS,NAMOUT)
      IFL=IFIND(IHNAM(NAH),NAMFLD,NFLDS)
      WRITE(6,29)NAH,NAMFLD(IFL),IFL,IFLD(IFL),FMN(NAH),FMX(NAH),
     +     FBIN(NAH),PMN(NAH),PMX(NAH),HTYP(NAH),HFIT(NAH),FREF(NAH,1),
     +     FREF(NAH,2)
   29 FORMAT(8X,'NAH= ',I4,4X,A8,2I4,5F8.2,4X,2A4,2F8.2)
C      IF(IFLD(IFL).LE.-1)THEN
C         WRITE(6,31)
C   31    FORMAT(1X,'*** THIS TYPE OF FIELD NOT ALLOWED ***')
C         NAH=NAH-1
C      END IF
      GO TO 20
      END
