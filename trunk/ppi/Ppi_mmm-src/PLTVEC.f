c
c----------------------------------------------------------------------X
c
      SUBROUTINE PLTVEC(DAT,IFLU,IFLW,IRSKIP,IASKIP,MNGATE,MXGATE,
     X                  NANG,BDVAL,XMN,XMX,ZMN,ZMX,USTRM,WSTRM,
     X                  VECSCL,ANG,H0,R0,DROLD,IVECCOL,MXR,MXA,MXF)
C
C  DRAW A VECTOR FROM TX,TZ AND SCALED BY VECSCL M/S PER KM
C     IFLU   - FIELD INDEX FOR U-COMPONENT
C     IFLW   - FIELD INDEX FOR W-COMPONENT

C     IRSKIP - RANGE GATE SKIPPING FACTOR
C     IASKIP - ELEVATION ANGLE SKIPPING FACTOR
C     USTRM - U COMPONENT OF STORM MOTION
C     WSTRM - W COMPONENT OF STORM MOTION
C    VECSCL - SCALED LENGTH OF THE ARROW HEAD
C
      DIMENSION DAT(MXR,MXA,MXF),ANG(MXA,2)
      COMMON /CONTOUR / LMX,LMN
      DATA SIN23,COS23,HSCL/0.391,0.921,0.25/
      DATA RE,REI/17000.0,1.17647E-04/
      DATA TORAD,TODEG/0.017453293,57.29577951/

      LMN=0.0
      LMX=0.0
      CALL SFLUSH
      CALL GSPLCI(IVECCOL)
      DO 100 J=1,NANG,IASKIP
         DO 90 I=MNGATE,MXGATE,IRSKIP
            U=DAT(I,J,IFLU)
            W=DAT(I,J,IFLW)
            IF(U.EQ.BDVAL.OR.W.EQ.BDVAL)GO TO 90
            SINA=SIN(TORAD*ANG(J,1))
            COSA=COS(TORAD*ANG(J,1))
            RNG=R0+(I-1)*DROLD
            TX=RNG*COSA
            TZ=H0+RNG*SINA+0.5*TX*TX*REI
            IF(TZ.LT.ZMN.OR.TZ.GT.ZMX)GO TO 90

            IF(TX.LT.XMN.OR.TX.GT.XMX)GO TO 90
            HX=TX+(U-USTRM)/VECSCL
            HZ=TZ+(W-WSTRM)/VECSCL
            IF(HX.LT.XMN.OR.HX.GT.XMX)GO TO 90
            IF(HZ.LT.ZMN.OR.HZ.GT.ZMX)GO TO 90
            DX=HX-TX
            DZ=HZ-TZ
            EX1=HX-HSCL*(DX*COS23-DZ*SIN23)
            EZ1=HZ-HSCL*(DZ*COS23+DX*SIN23)
            EX2=HX-HSCL*(DX*COS23+DZ*SIN23)
            EZ2=HZ-HSCL*(DZ*COS23-DX*SIN23)
            CALL LINE(TX,TZ,HX,HZ)
            CALL LINE(HX,HZ,EX1,EZ1)
            CALL LINE(HX,HZ,EX2,EZ2)
   90    CONTINUE
  100 CONTINUE

      CALL SFLUSH
      CALL GSPLCI(1)
      CALL GSTXCI(1) 
      RETURN
      END
