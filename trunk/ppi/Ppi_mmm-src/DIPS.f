c
c----------------------------------------------------------------------X
c
      SUBROUTINE DIPS(DIPV,INDX,DELTZ,DAT,NDIP,IMN,IMX,BDVAL,
     X     MXR,MXA,MXF)

C     ROUTINE TO CALCULATE THE MINIMUMS IN THE REFLECTIVITY DIFFERENCE
C     FIELD.  THE PROCEDURE IS TO START AT THE FAR EDGE OF THE STORM
C     (RELATIVE TO THE RADAR) AND MOVE TOWARD THE RADAR FINDING LOCAL
C     MINIMUMS IN THE DIFFERENCE FIELD ALONG THE WAY.  DIPS ARE FOUND
C     BY FINDING A CHANGE IN SIGN OF THE SLOPE OF THE DIFFERENCE FIELD.
C     TO BE COUNTED A LOCAL MINIMUM MUST BE LESS THAN THE NEIGHBORING
C     MINIMUM AT THE LONGER RANGE.

      DIMENSION DIPV(40),INDX(40),DELTZ(MXR),DAT(MXR,MXA,MXF)
      DIMENSION SLOPE(768)

C CALCULATE SLOPE OF DIFFERENCE FIELD

      DO 10 I=IMN,IMX
      SLOPE(I)=BDVAL
10    IF(DELTZ(I).NE.BDVAL.AND.DELTZ(I+1).NE.BDVAL)SLOPE(I)=
     +   DELTZ(I+1)-DELTZ(I)

C FIND DIPS IN DIFFERENCE FIELD BY SEARCHING FOR A CHANGE OF
C SIGN IN THE SLOPE OF THE DIFFERENCE FIELD

      DO 20 I=IMX,IMN+1,-1
      IF(SLOPE(I).NE.BDVAL.AND.SLOPE(I-1).NE.BDVAL)THEN
         IF(SLOPE(I).GT.0..AND.SLOPE(I-1).LT.0.)THEN
           IF(DELTZ(I).GT.0..AND.DELTZ(I).LT.DIPV(NDIP))THEN
              NDIP=NDIP+1
              INDX(NDIP)=I
              DIPV(NDIP)=DELTZ(I)
           END IF
         END IF
      END IF
20    CONTINUE
      NDIP=NDIP+1
      DIPV(NDIP)=0.
      INDX(NDIP)=IMN
      RETURN
      END
