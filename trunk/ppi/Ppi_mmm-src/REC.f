c
c----------------------------------------------------------------------X
c
      SUBROUTINE REC(MAX16B,NFLD,NGTS,NREC,NFLD1,I1,I2)

C     ROUTINE TO DETERMINE THE NUMBER OF PHYSICAL RECORDS (one or two)
C     REQUIRED TO HOLD A BEAM OF UNIVERSAL FORMAT DATA.
C
C     MAX16B-  Maximum number of 16-bit words allowed in a UF output
C              physical record.
C     NFLD  -  NUMBER OF OUTPUT UF FIELDS.
C     NGTS  -  NUMBER OF RANGE GATES.
C     NREC  -  ON OUTPUT, THE NUMBER OF PHYSICAL RECORDS NEEDED.
C     NFLD1 -  ON OUTPUT, THE NUMBER OF FIELDS IN THE FIRST AND
C              SECOND PHYSICAL RECORDS.
C     I1    -  ON OUTPUT, THE INDEX OF THE FIRST FIELD WRITTEN TO
C              THE FIRST AND SECOND (IF NEEDED) PHYSICAL RECORDS.
C     I2    -  ON OUTPUT, THE INDEX OF THE LAST FIELD WRITTEN TO
C              THE FIRST AND SECOND PHYSICAL RECORDS.

      DIMENSION NFLD1(2),I1(2),I2(2)

      I1(1)=1
      I2(1)=NFLD
      NFLD1(2)=0
      NFLD1(1)=NFLD
      NREC=1

C     CALCULATE THE NUMBER OF 16-BIT WORDS NEEDED TO HOLD
C     A BEAM OF DATA.  IF THE NUMBER IS LESS THAN MAX16B,
C     THEN ONLY ONE PHYSICAL RECORD IS REQUIRED.

10    NWDS=48+21*NFLD1(1)+NFLD1(1)*NGTS

      IF(NWDS.LE.MAX16B)RETURN

C     TWO PHYSICAL RECORDS ARE REQUIRED.  DECREMENT THE NUMBER
C     OF FIELDS IN THE FIRST RECORD BY ONE AND INCREMENT THE
C     NUMBER OF FIELDS IN THE SECOND RECORD BY ONE.  GO BACK TO
C     DETERMINE THE NUMBER OF WORDS AGAIN.

      NREC=2
      NFLD1(1)=NFLD1(1)-1
      NFLD1(2)=NFLD1(2)+1
      I2(1)=NFLD1(1)
      I1(2)=NFLD1(1)+1
      I2(2)=NFLD

      GO TO 10
      END
