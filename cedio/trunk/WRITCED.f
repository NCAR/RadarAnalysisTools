      SUBROUTINE WRITCED(SCINAME,BASANG,VOLNAM,IBEGYR,
     X     IBEGMNT,IBEGDAY,IBEGHR,IBEGMIN,IBEGSEC,IENDYR,
     X     IENDMNT,IENDDAY,IENDHR,IENDMIN,IENDSEC,XMIN,
     X     XMAX,NUMX,ISPCX,YMIN,YMAX,NUMY,ISPCY,ZMIN,
     X     ZMAX,NUMZ,ISPCZ,NFLD,FLDNAM,ISCLFLD,NUMLND,
     X     NUMRAD,NAMLND,XLND,YLND,ZLND,MXLND,IDAT,RADSTN,
     X     SOURCE,PROJECT,TAPE,RADCON,VNYQ,LATDEG,LATMIN,
     X     LATSEC,LONDEG,LONMIN,LONSEC)
C
C     SUBROUTINE TO WRITE OUT 3-D CARTESIAN DATASET IN
C     CEDRIC FORMAT. OUTPUT FILENAME WILL BE CALLED 'fort.30'.
C     YOU MUST SUPPLY ALL THE VARIABLES PASSED TO THIS SUBROUTINE
C     THIS ROUTINE WILL BUILD A CEDRIC HEADER FROM
C     THE VARIABLES YOU PASS IT. THE DECLARATIONS OF THE VARIABLES
C     ARE ALL LISTED BELOW. WHERE A VARIABLE IS NOT DECLARED BELOW, IT
C     HAS THE DEFAULT FORTRAN DECLARATION.
C     
C     HERE ARE THE MEANINGS OF THE VARIABLES:
C     
C     SCINAME: SCIENTIST NAME (CHAR STRING)
C      BASANG: ANGLE OF X-AXIS FROM TRUE NORTH (USUALLY 90.0)
C     VOLNAM:  NAME TO GIVE CEDRIC VOLUME (CHAR STRING)
C     IBEGYR:  BEGINNING YEAR OF VOLUME
C       .
C       .
C     IBEGSEC: BEGINNING SECOND OF VOLUME
C     IENDYR:  ENDING YEAR OF VOLUME
C       .
C       .
C     IENDSEC: ENDING SECOND OF VOLUME
C     XMIN   : MINIMUM X VALUE ON 3-D GRID (KM)
C     XMAX   : MAXIMUM X VALUE (KM)
C     NUMX   : TOTAL NUMBER OF X GRID POINTS
C     ISPCX  : SPACING BETWEEN GRID POINTS IN X (METERS)
C     YMIN   : MINIMUM Y VALUE ON 3-D GRID (KM)
C     YMAX   : MAXIMUM Y VALUE (KM)
C     NUMY   : TOTAL NUMBER OF Y GRID POINTS
C     ISPCY  : SPACING BETWEEN GRID POINTS IN Y (METERS)
C     ZMIN   : MINIMUM Z VALUE ON 3-D GRID (METERS) ***NOTE DIFF. UNITS***
C     ZMAX   : MAXIMUM Z VALUE (METERS) ***NOTE DIFF. UNITS***
C     NUMZ   : TOTAL NUMBER OF Z GRID POINTS
C     ISPCZ  : SPACING BETWEEN GRID POINTS IN Y (METERS)
C     NFLD   : NUMBER OF DATA FIELDS
C     FLDNAM : ARRAY OF CHAR. STRINGS CONTAINING NAMES OF FIELDS IN ORDER
C     ISCLFLD: ARRAY CONTAINING SCALE FACTORS FOR FIELDS. TYPICAL VALUE=100
C     NUMLND : NUMBER OF LANDMARKS (ORIGIN + NUM OF RADARS)
C     NUMRAD : NUMBER OF RADARS
C     NAMLND : ARRAY OF CHAR. STRINGS CONTAINING NAMES OF LANDMARKS
C              FIRST ONE MUST BE 'ORIGIN', OTHERS ARE NAMES OF RADARS
C     XLND   : ARRAY OF X COORDS. OF LANDMARKS, MATCHING ORDER IN NAMLND (KM)
C     YLND   : ARRAY OF Y COORDS. OF LANDMARKS, MATCHING ORDER IN NAMLND (KM)
C     ZLND   : ARRAY OF Z COORDS. OF LANDMARKS, MATCHING ORDER IN NAMLND (MET)
C     RADSTN : NAME OF RADAR STATION OR DATA ORIGIN
C     SOURCE : SOURCE OF DATA, E.G., FIELD TAPE NAME
C     PROJECT: NAME OF PROJECT OR EXPERIMENT
C     TAPE   : NAME OF TAPE
C     RADCON : RADAR CONSTANT
C     VNYQ   : NYQUIST VELOCITY
C     LATDEG : ORIGIN LATITUDE (DEGREE COMPONENT)
C     LATMIN : ORIGIN LATITUDE (MINUTE COMPONENT)
C     LATSEC : ORIGIN LATITUDE (SECOND COMPONENT)
C     LONDEG : ORIGIN LONGITUTE (DEGREE COMPONENT)
C     LONMIN : ORIGIN LONGITUTE (MINUTE COMPONENT)
C     LONSEC : ORIGIN LONGITUTE (SECOND COMPONENT)
C
      INCLUDE 'CEDRIC.INC'
      DIMENSION IDAT(MAXPLN)
      DIMENSION IBUF(MAXPLN)
      CHARACTER*4 PROJECT
      CHARACTER*6 SCINAME,NAMLND(MXLND),RADSTN,TAPE
      CHARACTER*8 VOLNAM,FLDNAM(MAXFLD),SOURCE
      DIMENSION ISCLFLD(MAXFLD),XLND(MXLND),YLND(MXLND),ZLND(MXLND)
      DATA ICNT/0/
      DATA SCFAC/1000.0/
C
C     Output unit is 10*L1+L2 = fort.30
C
      L1=3
      L2=0
      ISKP=0
C
C     SET POSITIONING INFO 1==> CREATE FILE 2==> APPEND
      IF (ICNT.EQ.0) THEN
         IPOS=1
         ICNT=1
      ELSE
         IPOS=2
      END IF
C
C     INITIALIZE OUTPUT FILE
C
      CALL COUT(L1,L2,IPOS,ISKP,IBUF)

      CALL HEADBLD(SCINAME,BASANG,VOLNAM,IBEGYR,
     X     IBEGMNT,IBEGDAY,IBEGHR,IBEGMIN,IBEGSEC,IENDYR,
     X     IENDMNT,IENDDAY,IENDHR,IENDMIN,IENDSEC,XMIN,
     X     XMAX,NUMX,ISPCX,YMIN,YMAX,NUMY,ISPCY,ZMIN,
     X     ZMAX,NUMZ,ISPCZ,NFLD,FLDNAM,ISCLFLD,NUMLND,
     X     NUMRAD,NAMLND,XLND,YLND,ZLND,MXLND,RADSTN,
     X     SOURCE,PROJECT,TAPE,RADCON,VNYQ,LATDEG,LATMIN,
     X     LATSEC,LONDEG,LONMIN,LONSEC)

      DO 100 I=1,NUMZ
C
C     CONSTRUCT AND WRITE OUT LEVEL HEADER
C
         CALL BLHED(IBUF,I,NUMZ)
C
C     LOOP OVER ALL FIELDS
C
         DO 200 IF=1,NFLD
C
C     LOAD IN **SCALED** DATA INTO ARRAY IDAT. IDAT WILL CONTAIN
C     DATA FOR A CONSTANT Z PLANE (PLANE I) FOR A GIVEN FIELD (FIELD IF).
C     THE DATA SHOULD BE ORDERED SO THAT THE X VALUES ARE VARYING FASTER
C     THAN THE Y VALUES. SEE THE DIMENSION STATEMENT FOR IDAT AT THE TOP OF 
C     THIS SUBROUTINE. YOU MUST SUPPLY THE ROUTINE FETCHPLANE. IT WILL TAKE
C     AS ARGUMENTS THE Z LEVEL NUMBER AND THE FIELD NUMBER AND WILL RETURN
C     THE X Y DATA FOR THAT LEVEL AND FIELD IN SCALED FORMAT.
C          
            XMN=XMIN
            YMN=YMIN
            ZMN=ZMIN/SCFAC
            DX=ISPCX/SCFAC
            DY=ISPCY/SCFAC
            DZ=ISPCZ/SCFAC
            CALL FETCHPLANE(IDAT,NUMX,NUMY,IF,I,XMN,YMN,ZMN,DX,DY,DZ)
C
C     PACK DATA
C
            CALL SBYTES(IBUF,IDAT,0,16,0,(NUMX*NUMY))
C
C     OUTPUT THE DATA TO DISK
C
            CALL CWRITE(IBUF,(NUMX*NUMY))
 200     CONTINUE
 100  CONTINUE


      RETURN

      END

                  
