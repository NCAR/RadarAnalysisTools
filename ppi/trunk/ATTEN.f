c
c----------------------------------------------------------------------X
c
      SUBROUTINE ATTEN(DAT,IOUT,IIN1,IIN2,C1,C2,BDVAL,MNGATE,MXGATE,
     X                 NANG,DROLD)

C  ROUTINE TO CALCULATE THE TWO-WAY TOTAL ATTENUATION USING THE METHOD OF
C  TUTTLE AND RINEHART (1983). THE METHOD CALCULATES THE ATTENUATION OF AN
C  ATTENUATING SIGNAL BY COMPARING IT WITH A NON-ATTENUATING SIGNAL.
C  THE DUAL-WAVELENGTH SIGNAL CAN BE CALCULATED
C  BY ADDING THE ATTENUATION FIELD TO THE ATTENUATING SIGNAL AND SUBTRACTING
C  THE RESULT FROM THE NON-ATTENUATING SIGNAL.
C  IIN1- POINTS TO THE NON-ATTENUATING SIGNAL
C  IIN2- POINTS TO THE ATTENUATING SIGNAL
C  IOUT- POINTS TO THE OUTPUT TOTAL ATTENUATION FIELD
C  C1- MINIMUM RANGE TO PROCESS
C  C2- MAXIMUM RANGE TO PROCESS
C  B - THE DEFAULT EXPONENT TO USE IN THE ATTENUATION-REFLECTIVITY
C      RELATION.  THE ATTENUATION CALCULATION IS NOT VERY SENSITIVE TO THE
C      CHOICE OF B.
C
      INCLUDE 'dim.inc'
      DIMENSION DAT(MXR,MXA,MXF),DIPV(40),INDX(40)
      DIMENSION DELTZ(MXR)

      DATA B/0.6/

      MN= NINT(C1/DROLD+1.0)
      MX= NINT(C2/DROLD+1.0)
      MN=MAX0(MNGATE,MN)
      MX=MIN0(MXGATE,MX)

      DO 200 IAZ=1,NANG

      DO 10 I=MN,MX

10       DAT(I,IAZ,IOUT)=BDVAL

C CALCULATE DIFFERENCE BETWEEN NON-ATTENUATING AND ATTENUATING SIGNALS

      DO 20 I=MN,MX
         DELTZ(I)=BDVAL
         IF(DAT(I,IAZ,IIN1).NE.BDVAL.AND.DAT(I,IAZ,IIN2).NE.BDVAL)
     +     DELTZ(I)=DAT(I,IAZ,IIN1)-DAT(I,IAZ,IIN2)
20    CONTINUE

C FIND FRONT EDGE OF ECHO (10 CONSECUTIVE GATES OF GOOD DATA)

      INUM=0
      IMN=-1
      DO 30 I=MN,MX
         IF(DAT(I,IAZ,IIN1).NE.BDVAL.AND.DAT(I,IAZ,IIN2).NE.BDVAL)THEN
            INUM=INUM+1
            IMN=I
            IF(INUM.GE.10)GO TO 40
         ELSE
            INUM=0
            IMN=-1
         END IF
30    CONTINUE
40    IMN=IMN-9

C     FIND FAR EDGE OF ECHO (10 CONSECUTIVE GATES OF GOOD DATA)

      IMX=-1
      INUM=0
      DO 50 I=MX,MN,-1
         IF(DAT(I,IAZ,IIN1).NE.BDVAL.AND.DAT(I,IAZ,IIN2).NE.BDVAL)THEN
            INUM=INUM+1
            IMX=I
            IF(INUM.GE.10)GO TO 60
         ELSE
            INUM=0
            IMX=-1
         END IF
50    CONTINUE
60    IMX=IMX+9
      IF(IMN.GT.0.AND.IMX.GT.0.AND.DELTZ(IMX).GT.0.)THEN

C     IF THE EDGE OF THE ECHO HAS BEEN FOUND AND THE ATTENUATION AT THE BACK EDGE
C     IS LARGER THAN ZERO, CALCULATE THE ATTENUATION BETWEEN PAIRS OF DIPS IN THE
C     DIFFERENCE FIELD.

         NDIP=1
         DIPV(NDIP)=DELTZ(IMX)
         INDX(NDIP)=IMX
         CALL DIPS(DIPV,INDX,DELTZ,DAT,NDIP,IMN,IMX,BDVAL)

C        LOOP OVER THE PAIRS OF DIPS FOUND IN THE DIFFERENCE FIELD.  FOR
C        EACH PAIR OF DIPS SUM UP THE REFLECTIVITY FIELD AND CALCULATE THE
C        COEFFICIENT (A0) FOR THE ATTENUATION-REFLECTIVITY RELATION. CALCULATE
C        THE ATTENUATION FROM THE ATTENUATION-REFLECTIVITY REALTION USING
C        A0 AND B.


         DO 92 I=1,NDIP-1
            ATT=DIPV(I)-DIPV(I+1)
            I1=INDX(I+1)
            I2=INDX(I)
            SUMZ=0.
            A0=0.0002
            DO 93 J=I1,I2
              IF(DAT(J,IAZ,IIN1).NE.BDVAL)THEN
                XT=10.**(DAT(J,IAZ,IIN1)/10.)
                SUMZ=SUMZ+XT**B
              END IF
93         CONTINUE
           SUMZ=SUMZ*DROLD
           IF(SUMZ.GT.0.)A0=ATT/SUMZ
           SUM=0.
           DO 94 J=I1,I2
             IF(DAT(J,IAZ,IIN1).NE.BDVAL)THEN
               XT=10.**(DAT(J,IAZ,IIN1)/10.)
               AT=A0*XT**B
               SUM=SUM+AT*DROLD
             END IF
94         DAT(J,IAZ,IOUT)=SUM+DIPV(I+1)
92       CONTINUE
      ELSE

C ELSE CALCULATE ATTENUATION USING THE STANDARD ATTENUATION-REFLECTIVITY
C RELATION.

         SUM=0.
         A0=0.0002
         DO 110 I=IMN,IMX-1
            IF(DAT(I,IAZ,IIN1).NE.BDVAL)THEN
              XT=10.**(DAT(I,IAZ,IIN1)/10.)
              AT=A0*XT**B
              SUM=SUM+AT*DROLD
            END IF
110      DAT(I,IAZ,IOUT)=SUM
      END IF

C     FILL THE OUTPUT ARRAY WITH THE CALCULATED ATTENUATION FIELD.

      DO 190 I=IMX,MXGATE
190      DAT(I,IAZ,IOUT)=DAT(IMX,IAZ,IOUT)
200   CONTINUE
      RETURN
      END
