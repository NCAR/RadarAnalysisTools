c
c----------------------------------------------------------------------X
c
      SUBROUTINE FXSWATH(DAT,IOUT,IIN1,C1,C2,BDVAL,MNGATE,MXGATE,NANG,
     X                 AZA,ITM,IAZC,FXOLD,FRSTBM,JBEAM,JBSWT,FXSWT,
     X                 TMP1,TMP2)
C
C  FUNCTION - FXSWATH: EXTRACT A PSEUDO-RHI BY AVERAGING ACROSS ALL BEAMS
C     THAT ARE INSIDE THE WINDOW (C1,C2), THEN STORE THIS AVERAGE IN BEAM
C     NUMBER (JBEAM) FOR LATER PLOTTING AS A TIME-TO-SPACE CONVERTED RHI.
C
C     IOUT   - OUTPUT FIELD NUMBER
C     IIN1   -  INPUT   "      "
C     DATIN1 - TEMPORARY VARIABLE TO ALLOW IOUT=IIN1
C     C1,C2  - AZIMUTH ANGLE WINDOW (MIN,MAX)
C     C3,C4  - ANGLE    "       "
C     JBEAM  - COUNTER FOR NUMBER OF BEAMS THAT HAVE BEEN ACCUMULATED
C     JBSWT  - CONTAINS TIME (SEC)  OF THE CURRENT BEAM WITHIN THIS SCAN
C     FXSWT  -    "     FIXED ANGLE  "  "     "      "     "     "    "
C
      LOGICAL IAZC,FRSTBM
      INCLUDE 'dim.inc'
      DIMENSION TMP1(MXR),TMP2(MXR,MXA)
      DIMENSION DAT(MXR,MXA,MXF),AZA(MXA,2),ITM(MXA,2)
      DIMENSION JBSWT(MXA),FXSWT(MXA),CNT(MXR)

      PAMN=C1
      PAMX=C2
      IF(FRSTBM)JBEAM=0

C     CLEAR ACCUMULATORS FOR AVERAGING OF BEAM TIME (SEC) AND ANGLE
C     AND FOR AVERAGING EACH RANGE GATE OF DATA
C
      NBEAM=0
      TSEC=0.0
      ANGLE=0.0
      DO 50 I=1,MXR
         CNT(I)=0.0
         TMP1(I)=0.0
   50 CONTINUE

C     SUM TIME AND ANGLE FOR ALL BEAMS WITHIN THE SECTOR (PAMN,PAMX)
C     AND SUM ACROSS BEAMS ALL DATA WITHIN SEPARATE RANGE GATES
C
      DO 100 J=1,NANG
         PANG=AZA(J,1)
         IF(PANG.LT.0.0)THEN
            TANG=PANG+360.0
         ELSE
            TANG=PANG
         END IF
         IF(TANG.GE.PAMN .AND. TANG.LE.PAMX)THEN
            NBEAM=NBEAM+1
            ANGLE=ANGLE+PANG

            IHR=ITM(J,1)/10000
            IMN=(ITM(J,1)-IHR*10000)/100
            ISC= ITM(J,1)-IHR*10000-IMN*100
            ISEC=ISC+60*IMN+3600*IHR
            TSEC=TSEC+ISEC
            DO 90 I=MNGATE,MXGATE
               IF(DAT(I,J,IIN1).NE.BDVAL)THEN
                  TMP1(I)=TMP1(I)+DAT(I,J,IIN1)
                  CNT(I)=CNT(I)+1.0
               END IF
   90       CONTINUE
         END IF
  100 CONTINUE

C     CALCULATE THE AVERAGES OF EACH RANGE GATE FOR THIS BEAM
C     AND STORE IN DAT(I,JBEAM,IOUT), UNLESS NO BEAMS IN WINDOW
C
      IF(NBEAM.GT.0)THEN
         JBEAM=JBEAM+1
         ANGLE=ANGLE/NBEAM
         IF(ANGLE.LT.0.0)ANGLE=ANGLE+360.0
         JBSWT(JBEAM)=NINT(TSEC/NBEAM)
         FXSWT(JBEAM)=FXOLD
      ELSE
         RETURN
      END IF

      DO 120 I=MNGATE,MXGATE
         IF(CNT(I).GT.0.0)THEN
            DAT(I,JBEAM,IOUT)=TMP1(I)/CNT(I)
         ELSE
            DAT(I,JBEAM,IOUT)=BDVAL
         END IF
  120 CONTINUE

      IF(JBEAM.GT.0)FRSTBM=.FALSE.

      RETURN
      END
