c
c----------------------------------------------------------------------X
c
      SUBROUTINE INIT1(ZSTR,COLRFIL,XRT,YTP,SIDEX,SIDEY,IGRPLT,BGFLAG,
     X                 LABX,LABY,DXL,DXR,DYB,DYT,NROW,NCOL,
     X                 XSHIFT,YSHIFT)
C
C  INITIALIZES THE PLOTTING WINDOW - MAPS USER COORDINATES TO DEVICE
C     OR FRACTIONAL COORDINATES
C
C     XRT,YTP   - DEVICE OR FRACTIONAL COORDINATES OF UPPER RIGHT HAND CORNER
C     SIDE      - MAXIMUM WIDTH OF PLOT REGION (0-1)
C     DXL,DXR   - LEFT AND RIGHT DEVICE (FRACTIONAL COORDINATES, 0 TO 1)
C     DYB,DYT   - BOTTOM AND TOP    "   (     "          "       "  " ")
C     GXMIN,MAX - MINIMUM AND MAXIMUM X-VALUES OF USER COORDINATES
C     GYMIN,MAX -    "     "     "    Y-VALUES  "   "       "
C     ZSTR      - STRETCHING FACTOR FOR Y PLOT AXIS (RHI ONLY)
C     Z         - RATIO OF X TO Y DISTANCES
C     X,Y SHIFT - SHIFTS ASSOCIATED WITH CENTERING OF MULTIPLE PLOTS/FRAME
C
      INCLUDE 'dim.inc'
      INCLUDE 'data.inc'
      INCLUDE 'input.inc'
      INCLUDE 'colors.inc'
      CHARACTER*4   IPFLD
      CHARACTER*28  LAB
      CHARACTER*6 IFMTX,IFMTY
      CHARACTER*1 BGFLAG

      LOGICAL  COLRFIL,FOF,UNI
      REAL NSMIN,NSMAX

      COMMON/BCN/RNGMIN,RNGMAX,TYMN,TYMX,IBSCAN,TYMSCL
      COMMON /INPUTC/ IPFLD
      COMMON /INPUTCH/NAMFLD(MXF),IRATYP,ICORD
      CHARACTER*8 NAMFLD,IRATYP,ICORD

C     Save all local variables between calls
C
      SAVE IFMTX,IFMTY,IPLX,IPLY,MJRX,MNRX,MJRY,MNRY

      DXR=XRT
      DXL=DXR-SIDEX
      DYT=YTP
      DYB=DYT-SIDEY
      IF(IBSCAN.NE.1)THEN
        IF(ITPOLD.NE.3)THEN
          ZRAT=1.0
        ELSE
          ZRAT=ZSTR
        END IF
        DX=GXMAX(ITPOLD)-GXMIN(ITPOLD)
        DY=GYMAX(ITPOLD)-GYMIN(ITPOLD)
        RATPL=DX/(DY*ZRAT)
        RATWN=SIDEX/SIDEY
        IF(RATPL.LT.RATWN)THEN
           DXL=DXR-SIDEX*RATPL/RATWN
        ELSE
           DYB=DYT-SIDEY*RATWN/RATPL
        END IF

C       CENTER MULTIPLE PLOTS PER FRAME
C              IN X: IF ONE COL, MULTIPLE ROWS
C              IN Y: IF ONE ROW, MULTIPLE COLUMNS
C
        XSHIFT=0.0
        YSHIFT=0.0
        IF(NCOL.EQ.1 .AND. NROW.GT.1)THEN
           DIFX=DXR-DXL
           DXROLD=DXR
           DXL=0.5-0.5*DIFX
           DXR=0.5+0.5*DIFX
           XSHIFT=DXROLD-DXR
        END IF
        IF(NROW.EQ.1 .AND. NCOL.GT.1)THEN
           DIFY=DYT-DYB
           DYTOLD=DYT
           DYB=0.5-0.5*DIFY
           DYT=0.5+0.5*DIFY
           YSHIFT=DYTOLD-DYT
        END IF
        CALL MAJMIN(GXMIN(ITPOLD),GXMAX(ITPOLD),IFMTX,MJRX,MNRX,IPLX)
        CALL MAJMIN(GYMIN(ITPOLD),GYMAX(ITPOLD),IFMTY,MJRY,MNRY,IPLY)
        CALL SET(DXL,DXR,DYB,DYT,GXMIN(ITPOLD),GXMAX(ITPOLD),
     +                           GYMIN(ITPOLD),GYMAX(ITPOLD),1)
      ELSE
         IHR1= IFTIME/10000
         IMN1=(IFTIME-IHR1*10000)/100
         ISC1= IFTIME-IHR1*10000-IMN1*100
         IHR2= ITIME/10000
         IMN2=(ITIME-IHR2*10000)/100
         ISC2= ITIME-IHR2*10000-IMN2*100
         ISCEND=ISC2+60*IMN2+3600*IHR2
         ISCBEG=ISC1+60*IMN1+3600*IHR1
         TYMSCL=FLOAT(ISCEND-ISCBEG)/(FLOAT(NANG(ISW))-1.0)
         CALL MAJMIN(TYMN,TYMX,IFMTX,MJRX,MNRX,IPLX)
         CALL MAJMIN(RNGMIN,RNGMAX,IFMTY,MJRY,MNRY,IPLY)
         CALL SET(DXL,DXR,DYB,DYT,TYMN,TYMX,RNGMIN,RNGMAX,1)
      END IF
      JLW=1000
      CALL SFLUSH
      CALL SETUSV('LW',JLW)
      CALL GSPLCI(1)
      CALL GSTXCI(1)
      CALL LABMOD(IFMTX,IFMTY,IPLX,IPLY,14,14,8,8,0)
      CALL TICK4(24,12,24,12)
      RETURN
c
c----------------------------------------------------------------------X
c
      ENTRY INIT2(ZSTR,COLRFIL,XRT,YTP,SIDEX,SIDEY,IGRPLT,BGFLAG,
     X            LABX,LABY,DXL,DXR,DYB,DYT,NROW,NCOL,XSHIFT,YSHIFT)

      JLW=1000
      CALL SFLUSH
      CALL SETUSV('LW',JLW)
      CALL GSPLCI(1)
      CALL GSTXCI(1)
      CALL LABMOD(IFMTX,IFMTY,IPLX,IPLY,14,14,8,8,0)
      CALL TICK4(24,12,24,12)
      CALL GRIDAL(MJRX,MNRX,MJRY,MNRY,LABX,LABY,5,0.,0.)
      CALL GETSET(FL,FR,FB,FT,UL,UR,UB,UT,LLL)
      CALL GRIDAL(MJRX,MNRX,MJRY,MNRY,LABX,LABY,10,UL,UB)
      RETURN
      END
