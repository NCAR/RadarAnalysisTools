c
c----------------------------------------------------------------------X
c
      SUBROUTINE HTOPO(DAT,IOUT,BDVAL,MNGATE,MXGATE,MANG,AZA,ELA,
     X                 XRD,YRD,R0,DROLD,AZROT,ITPOLD,MXR,MXA,MXF)
C
C  FUNCTION - HTOPO: F(OUT)=TOPOGRAPHIC HEIGHT OF HAWAIIAN ISLAND AS
C                           SEEN BY THE SCANNING RADAR.  HEIGHTS (M)
C                           ARE BUFFERED IN FROM UNIT 8 AND THEN
C                           INTERPOLATED TO (HRNG,AZIM) FOR CONTOURING.
C
C     OUTPUT TOPOGRAPHY IS IN KM, WITH SEA LEVEL SET TO BAD (-100.) SO
C     A HEIGHT OF 0.0 KM CAN BE RELIABLY CONTOURED.  THE INPUT FILE HAS
C     282 x 322 VALUES AT A SPACING OF 0.508 KM IN X AND Y.  CP4 IS NEAR
C     (I,J)=(221,190) IS THIS COORDINATE SYSTEM.
C
C     ITOPUN  - DEFAULT INPUT UNIT FOR TOPOGRAPHY IS UNIT 8
C     AZA,ELA - AZIMUTH AND ELEVATION ANGLES OF THE SCANNING RADAR
C     XRD,YRD - TRUE (X,Y) COORDINATES        "  "     "       "
C     R0,DROLD- RANGE (KM) AND GATE SPACING   "  "     "       "
C     AZROT   - ROTATION ANGLE OF SCAN (SEE RDFF AND RDUF)
C
C     IOUT   - OUTPUT FIELD NUMBER
C
      PARAMETER(MX=270,MY=310,MXM=MX+12,MYM=MY+12)
      DIMENSION DAT(MXR,MXA,MXF),AZA(MXA,2),ELA(MXA,2)
      DIMENSION ZGG(MXM,MYM),AZROT(8)
      LOGICAL INTOPO
      DATA INTOPO/.FALSE./
      DATA RE,TORAD,TODEG/17010.0,0.017453293,57.29577951/
      DATA IORG,JORG/221,190/
      DATA TXD,TYD/0.508,0.508/
      DATA ITOPUN,FLOOR,EPS/8,-100.0,1.0E-03/
C
C     LINEAR INTERPOLATION FORMULA FOR FUNCTION AT (X1.LE.X.LE.X2)
C
      FINT(X1,F1,X2,F2,X)=F1+(F2-F1)*(X-X1)/(X2-X1)
C
      IF(ITPOLD.GE.3 .AND. ITPOLD.LE.7)THEN
         WRITE(6,5)
    5    FORMAT(1X,'*** WARNING: TOPOGRAPHY NOT ALLOWED ***')
         DO 8 J=1,MANG
         DO 8 I=MNGATE,MXGATE
    8    DAT(I,J,IOUT)=BDVAL
         RETURN
      END IF
      TX1=TXD*(1-IORG)
      TX2=TXD*(MXM-IORG)
      TY1=TYD*(1-JORG)
      TY2=TYD*(MYM-JORG)

      IF(INTOPO)GO TO 20
C     BUFFER IN TOPOGRAPHY VALUES
C
      DO 10 I=1,MXM
      DO 10 J=1,MYM
   10 ZGG(I,J)=BDVAL
CANNE      BUFFER IN(ITOPUN,1) (ZGG(1,1),ZGG(MXM,MYM))
CANNE      IF(UNIT(ITOPUN)) 15,15,12
C   12 PRINT 13
C   13 FORMAT(1X,'*** PARITY ERROR READING TOPOGRAPHY FILE ***')
C      STOP
   15 CONTINUE
      INTOPO=.TRUE.
   20 CONTINUE

      DO 100 J=1,MANG
         SINA=SIN((AZA(J,1)-AZROT(ITPOLD))*TORAD)
         COSA=COS((AZA(J,1)-AZROT(ITPOLD))*TORAD)
         COSE=COS(ELA(J,1)*TORAD)
         DO 90 I=MNGATE,MXGATE
            DAT(I,J,IOUT)=BDVAL
            ZGRND=FLOOR
            SRNG=R0+(I-1)*DROLD
            HRNG=COSE*SRNG
            X=XRD+HRNG*SINA
            Y=YRD+HRNG*COSA
            IF((X.GE.TX1.AND.X.LE.TX2).AND.
     +         (Y.GE.TY1.AND.Y.LE.TY2))THEN
               IX=1.0001+(X-TX1)/TXD
               IY=1.0001+(Y-TY1)/TYD
               XE=TX1+IX*TXD
               XW=XE-TXD
               YN=TY1+IY*TYD
               YS=YN-TYD
               F1=ZGG(IX  ,IY  )
               F2=ZGG(IX+1,IY  )
               F3=ZGG(IX  ,IY+1)
               F4=ZGG(IX+1,IY+1)
               IF(F1.NE.BDVAL .AND. F2.NE.BDVAL .AND.
     +            F3.NE.BDVAL .AND. F4.NE.BDVAL)THEN
                  F12=FINT(XW,F1,XE,F2,X)
                  F34=FINT(XW,F3,XE,F4,X)
                  ZGRND=FINT(YS,F12,YN,F34,Y)
               END IF
            END IF
            IF(ZGRND.LE.EPS)THEN
               DAT(I,J,IOUT)=FLOOR
            ELSE
               DAT(I,J,IOUT)=ZGRND*0.001
            END IF
   90    CONTINUE
  100 CONTINUE
      RETURN
      END
