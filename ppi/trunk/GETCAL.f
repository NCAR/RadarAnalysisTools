c
c----------------------------------------------------------------------X
c
      SUBROUTINE GETCAL(INDAT,CTDBM,CTDBZH,CTDBMXH,CTDBMXV,CTDBXH,
     X                  CTDBXV,XHTHR,XVTHR)
C
C  READ IN THE APPROPRIATE CALIBRATION CURVES:
C     PROCESSOR COUNTS VS. FIELD IN METEOROLOGICAL UNITS (DBM)
C     DBMXV unfolding - If DBMXH > XHTHR and DBMXV < XVTHR, then unfold
C                       (add 256 to counts) the DBMXV values.
C
      DIMENSION CTDBM(255),CTDBZH(255),CTDBMXH(255),CTDBMXV(500),
     +          CTDBXH(255),CTDBXV(500)
      CHARACTER CALFILE*40,TITLE*60,INTITLE*80,NAMCAL*8
      CHARACTER*8 INDAT(10)
      DATA XHDEF,XVDEF/-65.,-100./

      CALFILE=INDAT(2)//INDAT(3)//INDAT(4)//INDAT(5)//INDAT(6)
      WRITE(6,5)CALFILE
    5 FORMAT(1X,'GETCAL: CALIBRATION FILE= ',A40)
      OPEN(UNIT=9,FILE=CALFILE,STATUS='OLD')

    6 READ(9,7,END=120)INTITLE
    7 FORMAT(A80)
      READ(INTITLE(1:8),9)NAMCAL
    9 FORMAT(A8)
      READ(INTITLE(11:20),10)RADCON
   10 FORMAT(F10.1)
      READ(INTITLE(23:80),11)TITLE
   11 FORMAT(A58)
      IF(NAMCAL.NE.'ENDCAL  ')THEN
         WRITE(6,13)TITLE
   13    FORMAT(/,1X,A60)
         WRITE(6,15)NAMCAL,RADCON
   15    FORMAT(1X,'CALIBRATION: ',A8,'  RADAR CONSTANT = ',F8.1)
      END IF

C     END OF ALL CALIBRATION CURVES
C
      IF(NAMCAL.EQ.'ENDCAL  ')THEN
         RETURN

C     PRIMARY WAVELENGTH RECEIVED POWER: COUNTS TO DBM (AND TO DBZ)
C
      ELSE IF(NAMCAL.EQ.'CT-DBM  ')THEN
         READ(9,17)(CTDBM(I),I=1,255)
   17    FORMAT(10X,F10.1)
         CALL DMPFLOAT(CTDBM,255)
         DO 20 I=1,255
   20    CTDBZH(I)=CTDBM(I)+RADCON

C     SECONDARY WAVELENGTH HORIZONTAL RECEIVED POWER: COUNTS TO DBM TO DBZ
C
      ELSE IF(NAMCAL.EQ.'CT-DBMXH')THEN
         READ(9,17)(CTDBMXH(I),I=1,255)
         CALL DMPFLOAT(CTDBMXH,255)
         DO 40 I=1,255
   40    CTDBXH(I)=CTDBMXH(I)+RADCON

C     SECONDARY WAVELENGTH VERTICAL RECEIVED POWER: COUNTS TO DBM TO DBZ
C
      ELSE IF(NAMCAL.EQ.'CT-DBMXV')THEN
C         READ(INTITLE(21:40),45,ERR=47)XHTHR,XVTHR
         READ(INDAT,43,ERR=47)XHTHR,XVTHR
 43      FORMAT(//////F8.0/F8.0)
         IF(XHTHR.EQ.0.)XHTHR=XHDEF
         IF(XVTHR.EQ.0.)XVTHR=XVDEF
 45      FORMAT(2F10.1)
         GO TO 48
 47      XHTHR=XHDEF
         XVTHR=XVDEF
 48      CONTINUE
         write(*,*)'getcal: Unfold DBMXV if DBMXH .gt.',xhthr,
     +        ' and DBMXV .lt.',xvthr
         DO 50 I=1,500
            READ(9,17,ERR=60,END=60)CTDBMXV(I)
 50      CONTINUE
 60      ILAST=I-2
         CALL DMPFLOAT(CTDBMXV,ILAST)
         DO 100 I=1,ILAST
            CTDBXV(I)=CTDBMXV(I)+RADCON
 100     CONTINUE
      END IF

      GO TO 6
 120  RETURN
      END
